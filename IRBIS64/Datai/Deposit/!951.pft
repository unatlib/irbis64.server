&uf('+7W1#'),&uf('+7W2#'),
(if v951<>'' then if v951^A : '.PDF' then &uf('+7W1#',v951) break fi fi),
/* В g1 - повторение 951 поля - кандидат на перенос в 955 поле
/* КОНТРОЛЬ: результат контроля в g2
if g1^A<>'' then 
if val(&uf('+95',g1^A))>64 then &uf('+7W2#1'),
                           else 
if (l("TXT="g1^A)>0) and (val(ref(l("TXT="g1^A),mfn))<>val(mfn)) 
                then &uf('+7W2#2'),
                else if (g1^A:',') or (g1^A:'"') or (g1^A:'__') 
                            then &uf('+7W2#3'),
                            else &uf('+7W3#',&uf('+9Y12,',&uf('+D'),',',g1^A)) if g3='0' 
                                  then &uf('+7W2#0')
                                  else if g3='-2' then &uf('+7W2#4'), 
                                                  else &uf('+7W2#5'), 
                                       fi,           
fi,
fi, 
fi,
fi, 
fi,
/*результат контроля в g2:
/* 0 - контроль положительный
/* 1 - длина имени файла больше допустимого
/* 2 - дублетное имя файла
/* 3 - имя файла содержит недопустимые символы
/* 4 - файл не найден
/* 5 - файл не поддается разбиению на страницы
if g2<>'0' then &uf('+7W1#') fi,
/* РЕЗУЛЬТАТ: В g1 повторение поля 951 для переноса в 955 или ПУСТОТА
&uf('+7W1#',&uf('+98/\',g1)),/* замена прямого слэша на обратный
g1,
/* ДОБАВЛЕНИЕ, связанное с автоматическим формированием ПРАВА ДОСТУПА 20.12.2018
&uf('+7W11#'),&uf('+7W12#'),&uf('+7W13#'),
/* g11 - идентификатор записи права доступа
/* g12 - срок действия права доступа
/* g13 - права доступа
if g1^C<>'' then 
/* есть СРОК ДЕЙСТВИЯ ПРАВА ДОСТУПА
&uf('+7W12#^E',g1^C),
fi,
if g1^L<>'' then 
/* есть ограничение права доступа ТОЛЬКО В ЛВС
&uf('+7W13#^A03^B',&uf('IMAIN,IPLVS,192.168.*.*'),'^C2'),
fi,
if g1^D<>'' then 
/* есть ограничения по категориям читателей
&uf('+7W2#',g1^D),&uf('+7W3#'),
(if &uf('Ag2#1')<>'' then &uf('+7W3#',&uf('G0,',&uf('Ag2#1'))),&uf('+7W2#',&uf('GC,',&uf('Ag2#1'))),&uf('+7U13#^A02^B',&uf('Ag3#1'),'^C2'),fi),
&uf('+7T13#0'),
/*(/g13),
fi,
/* ОПРЕДЕЛЕНИЕ: есть ли в БД RIGHT такое типовое право доступа
&uf('+7W11#',&uf('DRIGHT,!II=',g12,g13,'!,v1')),
if g11<>'' 
   then &uf('+7W12#'),&uf('+7W13#')  /* ЕСТЬ
   else 
/* НЕТ. Формирование ИДЕНТИФИКАТОРА НОВОЙ ЗАПИСИ ПРАВА ДОСТУПА
        (if &uf('Ag11#1')='' then &uf('+7W11#',&uf('R4')),if &uf('DRIGHT,!I=',&uf('Ag11#1'),'!,mfn')<>'' then &uf('+7W11#') fi, else break fi),
/*        /g11            
fi,

