/* 991^A - дата начала 991^B - дата конца
/* 991^Z - сортировка 1 - по датам 0 - по читателям
/*               1^A - БО для сортировки
/* g10^1 = 1^B - или дата или ФИО читателя для сортировки
/* g10^C = 1^C - ИД читателя
/* g10^D = 1^D - дата обращения
/* g10^G - имя БД ЭК
/* g10^F = 1^F - кол-во скаченных стр
/* g10^I =  1^I - шифр
/* g10^H = 1^H - имя текста
/* g10^K = 1^K - рейтинг
/* g10^3 = 1^3 - закладки
/*                1^Z - MFN в каталоге
&uf('+7W10#',,,&uf('7LICH,!TXT=',,&uf('+D'),,,"/"v955^A,,'!,@TextHistory40') ),,,,
/* g12 - первые 20 символов БО текущей БД каталога - для сортировки первого уровня
&uf('+7W12#',,'^A',&uf('+960.20#',,&uf('6BriefKoNew'), ) ),,,,,,,,,,
&uf('S0'),,
/* сортировка второго уровня по датам =1 или по читателям =0 ФИО всегда из RDR 
/* добавляю в g10^1 - или дата или ФИО читателя
if val(v991^Z)=1 
then 
   &uf('+7W10#',,(if p(g10) then '^1',,g10^D,,g10/ fi) ),&uf('+7T10') 
else 
   &uf('+7W10#',,(if p(g10) then '^1',,g10^Y,,g10/ fi) ),&uf('+7T10') 
fi,,
(if p(g10) 
 then 
   if &uf('+97',&uf('Av903#1'))=&uf('+97',g10^I) 
   then 
     if ( &uf('Av991^A#1')='' or val(g10^D)>=val(&uf('Av991^A#1')) ) and 
        ( &uf('Av991^B#1')='' or val(g10^D)<=val(&uf('Av991^B#1')) ) 
     then 
/* подполе B - для сортировки второго уровня
        if val(&uf('S1A'))=1 
        then 
           &uf('Ag12#1'),,'^B',g10^1,,'^C',g10^C,,'^D',g10^D,,'^I',g10^I,,'^F',g10^F,,'^K',g10^K,,'^H',g10^H,,,'^3',g10^3,,,,'^Z',mfn/
        else 
           &uf('Ag12#1'),,'^B',g10^1,,'^C',g10^C,,'^D',g10^D,,'^I',g10^I,,'^F',g10^F,,'^K',g10^K,,'^H',g10^H,,,'^3',g10^3,,,,/
        fi,/
     fi 
  fi 
fi /),,,
