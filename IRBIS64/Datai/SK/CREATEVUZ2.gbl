1
CreateVuz.wss
Определите способ корректировки
// приписка в записи дисциплин полей 83 по полям 691
// 991 - опрос
// ^A - связь с БД VUZ по имени дисциплины
// ^B - связь с БД VUZ по идентификатору дисциплины
// ^C - связь с БД VUZ по двум параметрам (имя + идентификатор)
IF
if val(v991^A)=0 and val(v991^B)=0 and val(v991^C)=0 then '1' fi
PUTLOG
'Не задан способ идентификации дисциплины в БД VUZ'
FI
IF
if val(v991^A)>0 or val(v991^B)>0 or val(v991^C)>0 then '1' fi
IF
if val(v991^A)>0 and val(v991^B)>0 then '1' fi
REP
991
1
v991,if val(v991^C)=0 then '^C1' fi

FI
DEL
1691
*


ADD
1691

(if p(v691) then if a(v691^U) then v691 fi fi/)

//================================цикл по полю 691 - каждый проход корректировка записи DISC
REPEAT
DEL
2691
*


ADD
2691

&uf('Av1691#1'),,,'^X',f(val(mfn),0,0)

IF
if val(v991^C)=0 then '1' fi
// задан один параметр
IF
if val(v991^A)>0 and val(v991^B)=0 or val(v991^A)=0 and val(v991^B)>0 then '1' fi
//------------------------- идентификация по имени или идентификатору
IF
if val(v991^A)>0 and val(&uf('JVUZ,DISCN=',v2691^D))>0 or val(v991^B)>0 and val(&uf('JVUZ,IDD=',v2691^I))>0 then '1' fi
// есть запись с именем дисциплины - дописывание поля 83
CORREC
'VUZ'
mpu,v2691
if val(v991^A)>0 then 'DISCN=',v2691^D else 'IDD=',v2691^I fi

// проверка по циклу, УК, кафедре, если они заданы
IF
if v1001^S='' or v1001^S=&uf('+97',v4) then if v1001^K='' or v1001^K=&uf('+97',v6) then if v1001^B='' or v1001^B=&uf('+97',v5) then '1' fi fi fi
// приписка с проверкой на уникальность
IF
if rsum((if p(v83) then if &uf('Av1001^S#1')='' or &uf('Av1001^S#1')=&uf('+97',v83^S) then if &uf('Av1001^K#1')='' or &uf('Av1001^K#1')=&uf('+97',v83^K) then if &uf('Av1001^B#1')='' or &uf('Av1001^B#1')=&uf('+97',v83^B) then if &uf('+97',v83^N)=&uf('Av1001^N#1') and &uf('+97',v83^C)=&uf('Av1001^C#1') and &uf('+97',v83^A)=&uf('Av1001^A#1') and &uf('+97',v83^V)=&uf('Av1001^V#1') and &uf('+97',v83^O)=&uf('Av1001^O#1') and &uf('+97',v83^F)=&uf('Av1001^F#1') then '1,' else '0,' fi fi fi fi fi))=0 then '1' fi
ADD
83

"^S"v1001^S,,"^B"v1001^B,,"^K"v1001^K,,"^W"v1001^W,"^N"v1001^N,"^C"v1001^C,"^A"v1001^A,"^V"v1001^V,"^O"v1001^O,"^F"v1001^F,"^E"v1001^E

PUTLOG
'Дописана запись дисциплины ',v1001^D,,' MFN=',f(val(mfn),0,0),,,' из записи ',,v1001^X
FI
FI
DEL
1001
*


END
FI
// конец идентификация по имени или идентификатору
FI
// конец задан один параметр
FI
IF
if val(v991^C)>0 then '1' fi
//----------------------идентификация по двум параметрам
IF
if val(&uf('JVUZ,DISCN=',v2691^D))>1 or val(&uf('JVUZ,IDD=',v2691^I))>1 then '1' fi
// случай неопределенности по имени - более одной записи
PUTLOG
'Дисциплина ',v2691^D,' более одной записи - отказ в создании/корректировки'
FI
IF
if val(&uf('JVUZ,DISCN=',v2691^D))>0 and val(&uf('JVUZ,IDD=',v2691^I))>0 then '1' fi
// есть запись с именем дисциплины - дописывание поля 83
//запись беру по идентификатору и проверяю имя дисциплины 
CORREC
'VUZ'
mpu,v2691
'IDD=',v2691^I

// проверка по циклу, УК, кафедре, если они заданы
IF
if v1001^S='' or v1001^S=&uf('+97',v4) then if v1001^K='' or v1001^K=&uf('+97',v6) then if v1001^B='' or v1001^B=&uf('+97',v5) then '1' fi fi fi
IF
if &uf('+97',v3^A)=v1001^D then '1' fi
// приписка с проверкой
IF
if rsum((if p(v83) then if &uf('Av1001^S#1')='' or &uf('Av1001^S#1')=&uf('+97',v83^S) then if &uf('Av1001^K#1')='' or &uf('Av1001^K#1')=&uf('+97',v83^K) then if &uf('Av1001^B#1')='' or &uf('Av1001^B#1')=&uf('+97',v83^B) then if &uf('+97',v83^N)=&uf('Av1001^N#1') and &uf('+97',v83^C)=&uf('Av1001^C#1') and &uf('+97',v83^A)=&uf('Av1001^A#1') and &uf('+97',v83^V)=&uf('Av1001^V#1') and &uf('+97',v83^O)=&uf('Av1001^O#1') and &uf('+97',v83^F)=&uf('Av1001^F#1') then '1,' else '0,' fi fi fi fi fi))=0 then '1' fi
ADD
83

"^S"v1001^S,,"^B"v1001^B,,"^K"v1001^K,,"^W"v1001^W,"^N"v1001^N,"^C"v1001^C,"^A"v1001^A,"^V"v1001^V,"^O"v1001^O,"^F"v1001^F,"^E"v1001^E

PUTLOG
'Дописана запись дисциплины ',v1001^D
FI
FI
FI
DEL
1001
*


END
FI
// конец оба параметра
FI
DEL
1691
1


UNTIL
if p(v1691) then '1' fi
//=======================================конец цикла по полю 691
DEL
2691
*


// конец задан параметр
FI
DEL
991
*


