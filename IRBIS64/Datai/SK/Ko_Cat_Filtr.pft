/* вырабатывает признак 1, если книга проходит фильтр
/* используется в статистике КО по специальности, по факультету, раздел 1 и 1.1
/* фильтрую при условии наличия экземпляров или ЭУ
/* ^8 - признак отбора книг за последние 5 лет
/* ^9 - кол-во лет - по файлу CycleNew.mnu G^S или 5 лет
/* ^X - признак скместра 0 или 9
/* проверка на эл.уч.
/* фильтр по выделенному фонду - 991^T
if  val(&uf('6KoElectro'))>0  
then 
   &uf('+7W7#1') 
else  
    &uf('+7W7#',f(rsum((if p(v910) then if &uf('Ag991^T#1')='' or s('!',v910^T,'!'): &uf('Ag991^T#1') then if v910^a:'U' then v910^1|,|,,else if '0 1 3 5 9': v910^a then '1,' ,,fi fi fi fi)),0,0) ),, 
fi,,,,,,,,,,,,,,,,,,,,,,
if val(G7)>0 
then
  if g991^T='' then else &uf('+7W7#',,,,if s(v910^T+| |): g991^T then '1' fi ),,fi,,
  if val(G7)>0 
  then 
    &uf('+7W1000#'),
    if g991^9='' 
    then
       if &uf('KCycleNew.mnu!',g991^S)<>'' 
       then &uf('+7W1001#',&uf('KCycleNew.mnu!',g991^S)) 
       else 
           if &uf('IDISPLAY,GRPart1YearNew,5')<>'' then &uf('+7W1001#',&uf('IDISPLAY,GRPart1YearNew,5') ) 
           else &uf('+7W1001#5') 
           fi,,,
       fi,,,
    else &uf('+7W1001#',g991^9) 
    fi,,,,,,
    if g991^8='' or 
       g991^8<>'' and 
       val(&uf('30')) - val(  (if p(v210) then if v210^D<>'' then &uf('+7W1#',v210^D) fi fi),,,G1  ) <=val(G1001) 
    then 
      (if p(v691) 
       then 
         if &uf('Ag991^D#1')='' or v691^D='' or &uf('+97',v691^D)=&uf('+97',&uf('Ag991^D#1')) then 
         if &uf('Ag991^S#1')='' or v691^S='' or &uf('+97',v691^S)=&uf('+97',&uf('Ag991^S#1')) then 
         if &uf('Ag991^O#1')='' or v691^O='' or &uf('+97',v691^O)=&uf('+97',&uf('Ag991^O#1')) then 
         if &uf('Ag991^V#1')='' or v691^V='' or &uf('+97',v691^V)=&uf('+97',&uf('Ag991^V#1')) then 
         if &uf('Ag991^A#1')='' or v691^A='' or &uf('+97',v691^A)=&uf('+97',&uf('Ag991^A#1')) then 
         if &uf('Ag991^C#1')='' or  s(v691^N,v691^C)='' or &uf('Ag991^C#1')<>'' and &uf('+97',v691^C)=&uf('+97',&uf('Ag991^C#1')) then 
         if &uf('Ag991^N#1')='' or  s(v691^N,v691^C)='' or &uf('Ag991^N#1')<>'' and &uf('+97',v691^N)=&uf('+97',&uf('Ag991^N#1')) then 
         if &uf('Ag991^g#1')='' or  v691^g: &uf('Ag991^g#1') then 
         if &uf('Ag991^F#1')='' or  v691^F='' or val(v691^F)=val(&uf('Ag991^F#1'))  then 
         if &uf('Ag991^X#1')='' or  
           &uf('Ag991^X#1')='0' and s(',0,1,3,5,7,9,11,13,'): s(|,|v691^F|,|) or
           &uf('Ag991^X#1')='9' and s(',2,4,6,8,10,12,14,'):  s(|,|v691^F|,|) then 
           &uf('+7W1000#1'),break,,
         fi fi fi fi fi fi fi fi fi fi,,
       fi/),G1000,,,
    fi,,,,
  fi,,,
fi

