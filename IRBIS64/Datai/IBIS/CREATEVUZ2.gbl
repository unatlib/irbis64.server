1
CreateVuz.wss
Определите способ корректировки
// приписка в записи дисциплин полей 83 по полям 691
// 991 - опрос
// ^A - связь с БД VUZ по имени дисциплины
// ^B - связь с БД VUZ по идентификатору дисциплины
// ^C - связь с БД VUZ по двум параметрам (имя + идентификатор)
IF
if v991^A='' and v991^B='' and v991^C='' then '1' fi
PUTLOG
'Не задан способ идентификации дисциплины в БД VUZ'
FI
IF
if v991^A<>'' or v991^B<>'' or v991^C<>'' then '1' fi
IF
if v991^A<>'' and v991^B<>'' then '1' fi
REP
991
1
v991,if v991^C='' then '^C1' fi

FI
DEL
1691
*


ADD
1691

(if p(v691) then if a(v691^U) then v691 fi fi/)

//================================цикл по полю 691 - каждый проход корректировка записи DISC
REPEAT
DEL
2691
*


ADD
2691

&uf('Av1691#1')

IF
if v991^C='' then '1' fi
// задан один параметр
IF
if v991^A<>'' and v991^B='' or v991^A='' and v991^B<>'' then '1' fi
//------------------------- идентификация по имени или идентификатору
IF
if v991^A<>'' and val(&uf('JVUZ,DISCN=',v2691^D))>0 or v991^B<>'' and val(&uf('JVUZ,IDD=',v2691^I))>0 then '1' fi
// есть запись с именем дисциплины - дописывание поля 83
CORREC
'VUZ'
mpu,v2691
if v991^A<>'' then 'DISCN=',v2691^D else 'IDD=',v2691^I fi
1
// приписка с проверкой
IF
if rsum((if p(v83) then if &uf('+97',v83^N)=&uf('Av1001^N#1') and &uf('+97',v83^C)=&uf('Av1001^C#1') and &uf('+97',v83^A)=&uf('Av1001^A#1') and &uf('+97',v83^V)=&uf('Av1001^V#1') and &uf('+97',v83^O)=&uf('Av1001^O#1') and &uf('+97',v83^F)=&uf('Av1001^F#1') then '1,' else '0,' fi fi))=0 then '1' fi
ADD
83

"^W"v1001^W,"^N"v1001^N,"^C"v1001^C,"^A"v1001^A,"^V"v1001^V,"^O"v1001^O,"^F"v1001^F

IF
if p(v83) then '1' fi
//------------------ по полям 83 макс.номер для УНД 
// в v112 будет макс.номер УНД 
DEL
112
*


ADD
112

'0'



// если есть среди полей 83 без УНД - формирую
DEL
1083
*


// повторения, в кот. нет УНД в 1083
ADD
1083

(if p(v83) then if a(v83^U) and a(v83^W) then v83 fi fi/)

DEL
83
F
(if p(v83) then if a(v83^U) and a(v83^W) then '1' else '0' fi fi/)

// определяю по полям 83 с какого номера формировать УНД
DEL
111
*


// из УНД все номера в 111
ADD
111

(if p(v83) then if p(v83^W) and a(v83^U) then &uf('G2/',v83^W) fi fi/)

IF
if p(v111) then '1' else '0' fi
REPEAT
REP
112
1
if val(&unifor('Av111#1'))>val(v112) then &unifor('Av111#1') else v112 fi

DEL
111
1


UNTIL
if p(v111) then '1' else '0' fi
FI

// формирую УНД в полях 1083 - для полей 83
ADD
111

(v1083/)

DEL
1083
*


// добавление в 1083 УНД
REPEAT
IF
if &unifor('Av111^W#1')='' and &unifor('Av111^u#1')='' then '1' else '0' fi
REP
111
1
'^W',v3^0,'/',f(val(v112)+1,0,0),&unifor('Av111#1')

REP
112
1
f(val(v112)+1,0,0)

FI
ADD
1083

&unifor('Av111#1')

DEL
111
1


UNTIL
if p(v111) then '1' else '0' fi
// дописываю поле 83 повторениями с прибавленными УНД
ADD
83

(v1083/)

DEL
1083
*


//------------------- конец - присутств. поле 83
FI
PUTLOG
'Дописана запись дисциплины ',v1001^D
FI
DEL
112
*


DEL
1001
*


END
FI
// конец идентификация по имени или идентификатору
FI
// конец задан один параметр
FI
IF
if v991^C<>'' then '1' fi
//----------------------идентификация по двум параметрам
IF
if val(&uf('JVUZ,DISCN=',v2691^D))>1 or val(&uf('JVUZ,IDD=',v2691^I))>1 then '1' fi
// случай неопределенности по имени - более одной записи
PUTLOG
'Дисциплина ',v2691^D,' более одной записи - отказ в создании/корректировки'
FI
IF
if val(&uf('JVUZ,DISCN=',v2691^D))>0 and val(&uf('JVUZ,IDD=',v2691^I))>0 then '1' fi
// есть запись с именем дисциплины - дописывание поля 83
//запись беру по идентификатору и проверяю имя дисциплины 
CORREC
'VUZ'
mpu,v2691
'IDD=',v2691^I
1
IF
if &uf('+97',v3^A)=v1001^D then '1' fi
// приписка с проверкой
IF
if rsum((if p(v83) then if &uf('+97',v83^N)=&uf('Av1001^N#1') and &uf('+97',v83^C)=&uf('Av1001^C#1') and &uf('+97',v83^A)=&uf('Av1001^A#1') and &uf('+97',v83^V)=&uf('Av1001^V#1') and &uf('+97',v83^O)=&uf('Av1001^O#1') and &uf('+97',v83^F)=&uf('Av1001^F#1') then '1,' else '0,' fi fi))=0 then '1' fi
ADD
83

"^W"v1001^W,"^N"v1001^N,"^C"v1001^C,"^A"v1001^A,"^V"v1001^V,"^O"v1001^O,"^F"v1001^F

PUTLOG
'Дописана запись дисциплины ',v1001^D
FI
FI
DEL
1001
*


END
FI
// конец оба параметра
FI
DEL
1691
1


UNTIL
if p(v1691) then '1' fi
//=======================================конец цикла по полю 691
DEL
2691
*


// конец задан параметр
FI
DEL
991
*


