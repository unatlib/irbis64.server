/* Условные операторы расставлены таким образом, чтобы наиболее часто встречающиеся значения, -  ключи с MFN для вывода записи обрабатывались в первую очередь, дополнение списка специализаций во вторую и заголовки (дисциплины) - в третью. Т.е. расположение условных операторов обратно последовательности вывода на бумагу(в файл)
 
/* ГП 778 - номера уже выведенных в текущем разделе записей
/* ГП 777 - специальности, для которых читает указанная пользователем кафедра выведенные дисциплины

(if p(v1) then, 
 #
 if p(v1^c) and &uf('+1R777')=''   then, 

  if &uf('+1R778'):v1^c then else 
   &uf('D,@',v1^c,',@tabkfsb'),
   &uf('+1W778#',&uf('+1R778'),v1^c)
  fi,

 else,  
  #
  if a(v1^c) and p(v1^d) then 
   &uf('+1W777#',if &uf('+1R777')<>'' then &uf('+1R777') ', ' fi, v1^d,),  

  else,
   /* После каждого заголовка выводятся специальности и шапка таблицы с БО
   if  p(v1^c) then, 
   #
   ' \qc \par\fs22 \qc { \qc (', &uf('+1R777'),') } ', 

   '\par\par \b0 \fs20 ', 
'\trowd \trqc\trgaph108\trleft-108\trhdr\cellx459\cellx6562\cellx7696\cellx9114\pard \qc\widctlpar\intbl {\b N \cell }\pard \qc\widctlpar\intbl {\b Заглавие учебника\cell }\pard   \qc\widctlpar\intbl {\b Осн/Доп\cell }\pard \qc\widctlpar\intbl {\b Кол-во экз-ров\cell }\pard  \widctlpar\intbl {\b \row }\pard \qc\widctlpar', 
    &uf('+1W777#'), 
    #
    &uf('D,@',v1^c,',@tabkfsb'),
   /* В ГП 778 записываются инвентарные номера тех записей, которые 
   /* уже выводились в текущем разделе(под заголовком дисциплины).    
   /* Повторно в том же разделе запись не выводится 
     &uf('+1W778#',&uf('+1R778'),v1^c)
    else,
    ##
  '\pard\tab\par\s1\b\fs32 \qc ' 
 ,v1^a, 
     &uf('+1W778#'),
    fi,
   fi,
  fi,
fi),  
