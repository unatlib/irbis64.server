0
IF
if v920: 'AB' then '1' fi
//==================================перенос абонента
IF
if s(v40,v41,v42,v39,v35)='' then '1' fi
PUTLOG
'Абонент ',v100,' - нет данных для архивации'
FI
IF
if s(v40,v41,v42,v39,v35)<>'' then '1' fi
IF
if val(&uf('JMBA_ARH,ABY=',,v100,'-',&uf('30')))>0 then '1' fi
PUTLOG
'Абонент ',,v100,,' год ',,&uf('30'),,' - архив уже создан'
FI
IF
if val(&uf('JMBA_ARH,ABY=',,v100,'-',&uf('30')))=0 then '1' fi
//-----------создаю новую запись
NEWMFN
'MBA_ARH'
ADD
100

v100

ADD
300

&uf('30')

ADD
40

(if p(v40) then if v40^F.1='*' then else if v40^F<>'' then v40 fi fi fi/)

ADD
41

v41

ADD
42

(v42/)

ADD
39

(v39/)

ADD
35

v35

ADD
920

'AB'

PUTLOG
'Абонент ',,v100,,' - создана архивная запись'
END
//------------удаляю поля в записи абонента
DEL
40
F
(if p(v40) then if v40^F.1='*' then '0' else '1' fi fi/)

DEL
41
*


ADD
41

v35

DEL
39
*


DEL
35
*


DEL
42
*


//----------конец создания записи
FI
//-----------конец есть данные для архивации
FI
//-------------конец абонента
FI
IF
if v920: 'ODER' then '1' fi
//==================================перенос заказа
IF
if &uf('+97',&uf('Av942^A#',,&uf('+N942') )) = &uf('+97',&uf('IRequest,MBASTATUSITOG,H1')) then else '1' fi
PUTLOG
'Заказ ',,v30^A,,' - не перенесен, последний статус не "Итоговое списание"'
FI
IF
if &uf('+97',&uf('Av942^A#',,&uf('+N942') )) = &uf('+97',&uf('IRequest,MBASTATUSITOG,H1')) then '1' fi
IF
if val(&uf('JMBA_ARH,QABY=',,v100,'-',v30^A,,'-',&uf('30')))>0 then '1' fi
'Абонент ',,v100,,' заказ ',v30^A,,,' год ',,&uf('30'),,' - архив уже создан'
FI
IF
if val(&uf('JMBA_ARH,QABY=',,v100,'-',v30^A,,'-',&uf('30')))=0 then '1' fi
NEWMFN
'MBA_ARH'
ALL
ADD
300

&uf('30')

PUTLOG
'Заказ ',,v30^A,,' - создана архивная запись'
END
DELR




//----------конец проверки уже есть
FI
//-------конец проверки последнено статуса
FI
//-------------конец заказа
FI
