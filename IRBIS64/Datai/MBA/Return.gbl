0
// возврат по МБА
// запускается на записи абонента, у которой есть поле 40
// 991^A - шифр в каталоге
// 991^D - дата выдачи
// 991^E - дата предп.возврата
// 991^F - дата возврата
// 991^H - ИН/ШК экземпляра
// 991^G - имя БД каталога
// 991^B - номер заказа
// 991^Y - признак возврата в каталоге
IF
if p(v991) then '1' fi
//---возврат в записи абонента
DEL
1991
*


ADD
1991

(v991/)

//---------------------------------------------цикл по заданным полям 991
REPEAT
DEL
991
*


ADD
991

&uf('Av1991#1')

//----------в записи абонента
REP
40^F
F
(if p(v40) then if v40^F<>'' then if &uf('+97',v40^B)=&uf('Av991^B#1') then if v40^F.1='*' and v40^D=&uf('Av991^D#1') then if &uf('+97',v40^H)=&uf('Av991^H#1') and &uf('+97',v40^G)=&uf('Av991^G#1') then &uf('3'),,,&uf('+7W10#',,v40^B,)  else v40^F, fi else v40^F fi else v40^F fi else # fi fi/)

IF
if g10<>'' then '1' fi
PUTLOG
'Оформлен возврат у абонента для заказа ',g10
//----------возврат в записи заказа
CORREC
'*'
v991
'QAB=',v100," - "v991^B

IF
if &uf('Av942^A#',,&uf('+N942') ): 'S' then '0' else '1' fi
ADD
942

if val(v1001^E)<val(&uf('3')) then '^A',,&uf('IRequest,MBASTATUSRETN,S2') else '^A',,,&uf('IRequest,MBASTATUSRETNNOT,S1') fi,,,'^C',,&uf('3'),,

PUTLOG
'Оформлен возврат в записи заказа ',g10
FI
DEL
1001
*


END
//------------------------------------------------------- в запись каталога
IF
if val(&uf('IMAIN,CorrCatAble,0'))=1 and  val(v991^Y)=1 then '1' fi
IF
if v991^G='' then '1' fi
PUTLOG
'Заказ ',,v991^B,,' - не задано имя БД каталога'
FI

IF
if v991^A='' then '1' fi
PUTLOG
'Заказ ',,v991^B,,' - не задан шифр в БД каталога'
FI

IF
if v991^G<>'' and v991^A<>'' then '1' fi,,,

IF
if val(&uf('D',v991^G,',!I=',,,v991^A,,'!,mfn'))<=0 then '1' fi
PUTLOG
'БД ',,v991^G,,': не найдена запись в БД каталога, заказ ',,v991^B,,
FI

CORREC
v991^G
mpu,v991
'I=',,v991^A

// 1001^H - ИН/ШК экземпляра
ADD
1001

&uf('+7W10#')

REP
910^A
F
(if p(v910) then if p(v910^A) then if s(v910^B,v910^H)<>'' then if &uf('+97',v910^B)=&uf('Av1001^H#1') or &uf('+97',v910^H)=&uf('Av1001^H#1') then if s('U C'): v910^A then v910^A else '0',,,&uf('+7W10#1') fi else v910^A fi else v910^A fi else # fi fi/)

IF
if val(g10)=0 then '1' fi
REP
910^2
F
(if p(v910) then if p(v910^2) then if s(v910^B,v910^H)<>'' then if &uf('+97',v910^B)=&uf('Av1001^H#1') or &uf('+97',v910^H)=&uf('Av1001^H#1') then if s('U C'): v910^A then if val(v910^2)>1 then f(val(v910^2)-1,0,0),,&uf('+7W10#2') else '0' fi else v910^2 fi else v910^2 fi else v910^2 fi else # fi fi/)

IF
if val(g10)=2 then '1' fi

REP
910^1
F
(if p(v910) then if p(v910^1) then if s(v910^B,v910^H)<>'' then if &uf('+97',v910^B)=&uf('Av1001^H#1') or &uf('+97',v910^H)=&uf('Av1001^H#1') then if s('U C'): v910^A then f(val(v910^1)+1,0,0),,&uf('+7W10#3')  else v910^1 fi else v910^1 fi else v910^1 fi else # fi fi/)

FI
FI
IF
if val(g10)>0 then '1' fi
PUTLOG
'БД ',,v1001^G,,,,': возврат ',,,,v1001^H,,,,,' в записи каталога MFN=',,f(val(mfn),0,0)
FI
DEL
1001
*


END
//--конец задано имя каталога и шифр
FI
//--конец возврата в записи каталога
FI
//---возврат у читателя, если разрешен
IF
if val(&uf('IMAIN,CorrRdrAble,0'))=1 then '1' fi
IF
if val(&uf('DRDR,!SH=',,,v991^A,,,'!,mfn'))<=0 then '1' fi
PUTLOG
'БД RDR: не найдена запись по шифру ',,v991^A,,
FI
//----------------------------------------------в запись читателя
CORREC
'RDR'
mpu,,v991,,,&uf('+7W10#')
'SH=',,,v991^A

REP
40^F
F
(if p(v40) then if v40^F<>'' then if &uf('+97',v40^B)=&uf('Av1001^H#1') or &uf('+97',v40^H)=&uf('Av1001^H#1') then if v40^F.1='*' then if  &uf('+97',v40^G)=&uf('Av1001^G#1') then  &uf('3'),,,&uf('+7W10#',,&uf('Av1001^H#1'),)  else  v40^F, fi else v40^F fi else v40^F fi else # fi fi/) 

IF
if g10<>'' then '1' fi
PUTLOG
'БД RDR: возврат ',,g10,,' в записи читателя MFN=',,f(val(mfn),0,0)
FI
DEL
1001
*


END
//---конец возврата у читателя
FI
//-- конец задан номер заказа
FI
DEL
1991
1


UNTIL
if p(v1991) then '1' fi
FI
DEL
991
*


