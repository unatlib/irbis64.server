/* g10 - результат контроля: если пусто - ДА
&uf('+7W10#'),
if (l("I="v1)<=0) or (val(ref(l("I="v1),mfn))=val(mfn)) 
   then if (v3<>'') and (v1<>'') then if v3:'*' then  else &uf('+7W10#1 Шаблон не содержит переменную числовую часть (символы *)' ) fi 
                  else if v3='' then &uf('+7W10#1 Отсутствует поле ШАБЛОН ') else &uf('+7W10#1 Отсутствует поле ИНДЕКС ') fi 
        fi 
   else &uf('+7W10#1 Дублетный индекс ') v1,ref(l("I="v1),' (См. N ',f(val(mfn),0,0),')') 
fi/
if g10='' then
    if (v3:'**') and (&uf('+95',v2)<>&uf('+95',v3)) then &uf('+7W10#1 Несовпадение длины шаблона и счетчика') fi ,
fi 
if g10='' then 
    &uf('+7W3#',&uf('G0*',v3)),
    /* g3 - префиксная часть шаблона 
    if (g3<>'') and (&uf('+9S!',g3,'!',v2)<>'1') then &uf('+7W10#1 Несовпадение префиксной части шаблона и счетчика') fi  
fi 
if g10='' then 
    &uf('+7W4#',&uf('G1*',v3)),/* g4 - часть шаблона без префиксной части
    (if &uf('+960.1#',&uf('Ag4#1'))='*' then &uf('+7W4#',&uf('+960*1.100#',&uf('Ag4#1'))) else break fi),
    /* g4 - суффиксная часть шаблона
    &uf('+7W5#',&uf('+95',g4))
    /* g5 - длина суффиксной части шаблона          
    if (g4<>'') and (&uf('+961.',g5,'#',v2)<>g4) then &uf('+7W10#1 Несовпадение суффиксной части шаблона и счетчика') fi 
fi 
if g10='' then 
    &uf('+7W6#',&uf('+95',g3)), /* g6 - длина префиксной части шаблона
    &uf('+7W7#',&uf('+960*',g6,'.100#',v2)),&uf('+7W7#',&uf('+961*',g5,'.100#',g7)), /* g7 - числовая часть счетчика
    (if &uf('Ag7#1')<>'' then  if '0123456789':&uf('+960.1#',&uf('Ag7#1')) then &uf('+7W7#',&uf('+960*1.100#',&uf('Ag7#1'))) else &uf('+7W10#1 Числовая часть счетчика содержит не цифры'),break fi fi), g7 /* проверка на цифры
fi         
if g10='' then '0' else g10 fi 
