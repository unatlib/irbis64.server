'{',if v920:'MUSP' THEN '\tab \b    Описание музейного предмета \par \b0 ','\PARD\TX4',mhl,   
"\par \b Общее обозначение \b0 : "v461, 
'\par \b Название\b0 :\tab ',v200^v,if p(v200^v)then" : "v200^a else v200^a fi, 
if s(v700,v961)<>'' then '\par \b Автор \b0 :\tab ',v961,v700, fi,
If p(v702) then '\par \b Лица, имеющие отношение \b0 :\tab ',v702^a,' ',v702^b,'(\b ', &unifor('F1'v702^4),'\b0 )' fi  
IF s(v962,v710)<>'' then'\par \b Коллектив: \b0 \tab 'v962^s".",v962^a, 
   if s(v962^n,v962^c)<>''then' ('v962^n" ; ",v962^c,')'fi,". "v962^b, 
   if s(v962^d,v962^f,v962^e)<>''then' ('v962^d" ; ",v962^f, 
      if p(v962^f)then" ; "v962^e else v962^e fi," / "v962^h," / "v962^i')' 
   fi,". "d962,
   if a(v962) then v710^s".",v710^a, 
      if s(v710^n,v710^c)<>''then' ('v710^n" ; ",v710^c,')'fi,". "v710^b, 
      if s(v710^d,v710^f,v710^e)<>''then' ('v710^d" ; ",v710^f, 
         if p(v710^f)then" ; "v710^e else v710^e fi," / "v710^h," / "v710^i')' 
      fi,". "d710,
fi fi,
'\par \b Тип предмета\b0 :\tab ',if p(v116) then &unifor('K116a0.mnu|'v116^b) ', ' fi,d900^x,  
 &unifor('Kznm.mnu|'v900^x), 
'\par \b Идентификационный номер \b0 : ',if v19^a:'z' then else v19^a.3| | fi,v19^b, 
"\par \b Параллельные названия\b0 : "| = |+v510^d,|, |v924, 
 if p(v210) then '\par \b Датировка\b0 : 'v210^d,fi,' ',v210^5,   
 if p(v215) then '\par \b Колич.характеристики\b0 : 'v215^a,", "v215^4,
 if p(v215^z) then ", "d215^a fi,"состоит из "v215^z" единиц хранения"  fi,
 if p(v488) then ,Opris488, fi,
 if p(v10^d) then '\par \b Стоимость закупочная\b0 : \tab ',v10^d fi,if p(v10^d) then ' ',v10^c,if a(v10^c) then &unifor('Korg.mnu|3') fi fi, 
 if p(v10^1) then '\par \b Стоимость страховая\b0 : \tab ',v10^1 fi,if p(v10^1) then ' ',v10^c,if a(v10^c) then &unifor('Korg.mnu|3') fi fi, 
 if p(v391) then '\par \b Примечания о наличии автографов\b0 : ',|, |+v391 fi, 
 If p(v300) then '\par \b Общие примечания\b0 : ',|, |+v300 fi, 
 if p(v397) then '\par \b Примечания о технике изготовления\b0 : ',|, |+v397 fi, 
  ,oant, mhl, 
 if p(v398) then '\par \b Клейма, штампы\b0 : ',|, |+v398 fi,     
 if p(v395) then '\par \b Надписи, даты\b0 : ',|, |+v395 fi,     
 if p(v912) then '\par \b Примечания о языке\b0 : ',|, |+v912 fi,     
 if p(v399) then '\par \b Состояние предмета\b0 : ',|, |+v399 fi,     
 if p(v430) then '\par \b Переатрибуция\b0 : ',|, |+v430 fi,     
 if p(v470) then '\par \b Публикация\b0 : ',|, |+v470 fi,
 if p(v951) then'\par См. ссылк',
    if rsum((if s(v951^i,v951^a)<>''then'1;'else'0;'fi))>1 then'и:   'else'у 'fi,
    (if p(v951) then |,  |+d951,
        if p(v951^a) then &unifor('+I$3,10,,'v951^a'$',v951^t,| ФАЙЛ|n951^t) fi 
        if p(v951^i) then &unifor('+I$0,,'v951^i'$',v951^t,| INTERNET|n951^t) fi fi) 
 fi,
 if v903.1:'-' then else'\par\b Шифр предмета в базе данных\b0 :'v903 fi,    
 if p(v607)then'\par \b Этническая классификация: \b0   ' 
    (if p(v607)then |\par   |+v607^a,| -- |v607^b,| -- |v607^c,| -- |v607^d,| -- |v607^g, 
    | -- |v607^e,| -- |v607^o,if v607=v607^h then v607^h else |, |v607^h fi fi) fi, 
 if p(v606) then '\par \b Тематическая классификация: \b0 '(| ; |+d606^a,v606^a,
    if &unifor(|Kktt.mnu\|v606^a)<>'' then' (',&unifor(|Kktt.mnu\|v606^a)')' fi) fi, 
 "\par \b Дескрипторы: \b0  "v965+| -- |, 
 if &unifor('Av905^f#1')<>''or &unifor('IPRIVATE,FPK,')<>''then else 
 "\par \b Ключевые слова (''Своб.индексиров.''): \b0 "d610,v610+|--|, 
 fi, 
 if p(v902) then '\par \b Держатель предмета: \b0 'v902^a fi, 
 '\par \b Дата поступления:  \b0 ',v910^c, 
 if p(v910^6) then '\par \b Способ поступления: \b0 ',&unifor('Kpst.mnu|'v910^6) fi, 
 if p(v910^t) then '\par \b История поступления: \b0 ',v910^t fi, 
 if p(v910^j) then '\par \b Характер поступления: \b0 ',&unifor('Kcharp.mnu|'v910^J) fi, 
 if p(v910^f) then '\par \b Канал поступления: \b0 ',&unifor('Kkpm.mnu|'v910^f) fi, 
 if p(v910^d) then '\par \b Место хранения: \b0',&unifor('Kmhrm.mnu|'v910^d)fi, 
,mpl,fi'}', 
