/* &uf('+7W1001#^F2^IИнф1'),
/* 10 - студенты ВС для дисц-ФО (>0 - условие для расчета),
/* 1011 - экз. OС 
/*    для выделенного фонда,заданного явно (991^t) или 
/*    для заданной ФО (1001^o) или 
/*    экз-ры НЕ выделенные (не задано ничего)
/* 10131 - число студентов ОС всего для заданной ФО
/* 1013 - число студентов ОС всего
/* 10111 - число экз. ОС НЕ выделенных фондов 
/* 20 - число оригинальных выделенных фондов (условие добавки к КО по выделенным фондам) 
/* 1110, 1111 - промежуточные
/* 2001 - результат для формы KKODISCS
&uf('+7W10#'),&uf('+7W10131#'),&uf('+7W1013#'),&uf('+7W1011#'),&uf('+7W10111#'),&uf('+7W20#'),
&uf('+7W1110#'),&uf('+7W1111#'),
      if val(&unifor('IMAIN,ACCESSRDR,1'))=1 then 
         &unifor('+7W1110#',
            (if p(v691) then 
                if (&uf('Ag1001^f#1')<>'' and v691^f=&uf('Ag1001^f#1') or &uf('Ag1001^f#1')='') and 
                   (&uf('Ag1001^a#1')<>'' and &uf('+97',v691^a)=&uf('+97',&uf('Ag1001^a#1')) or 
                    &uf('Ag1001^a#1')='') and 
                   (&uf('Ag1001^i#1')<>'' and &uf('+97',v691^i)=&uf('+97',&uf('Ag1001^i#1')) or 
                    &uf('Ag1001^i#1')='') and 
                   (&uf('Ag1001^c#1')<>'' and &uf('+97',v691^c)=&uf('+97',&uf('Ag1001^c#1')) or 
                    &uf('Ag1001^c#1')='')  
                then 
                   if s(&uf('Ag991^o#1'),&uf('Ag1001^o#1'))='' or 
                      &uf('Ag991^o#1')<>'' and &uf('+97',v691^o)=&uf('+97',&uf('Ag991^o#1')) or 
                      &uf('Ag1001^o#1')<>''and &uf('+97',v691^o)=&uf('+97',&uf('Ag1001^o#1'))
                   then 
                      if p(v691^0) then 
                         &unifor('7RDR,!UPS='v691^0,'!,mfn/') 
                         &unifor('7RDR,!?',v691^0,'!,mfn/') 
                         &unifor('7RDR,!',&unifor('G2*'v691^0),'!,mfn/') 
             fi fi fi fi)),&uf('+7G1110'),
         &uf('+7W10#'f(rsum((if p(g1110) then '1;'fi/)),0,0))                      
      else 
         &uf('+7W10#'
             f(rsum((if p(v691) then 
                        if (&uf('Ag1001^f#1')<>'' and v691^f=&uf('Ag1001^f#1') or &uf('Ag1001^f#1')='') 
                        and 
                           (&uf('Ag1001^a#1')<>'' and &uf('+97',v691^a)=&uf('+97',&uf('Ag1001^a#1')) or 
                            &uf('Ag1001^a#1')='') and 
                           (&uf('Ag1001^i#1')<>'' and &uf('+97',v691^i)=&uf('+97',&uf('Ag1001^i#1')) or 
                            &uf('Ag1001^i#1')='') and 
                           (&uf('Ag1001^c#1')<>'' and &uf('+97',v691^c)=&uf('+97',&uf('Ag1001^c#1')) or 
                            &uf('Ag1001^c#1')='')  
                        then 
                           if &uf('Ag991^o#1')='' or &uf('+97',v691^o)=&uf('+97',&uf('Ag991^o#1')) then 
                              if p(v691^0) then 
                                 &unifor('DVUZ,"!',v691^0,'",v68^Z')';' 
                     fi fi fi fi)),0,0)), 
      fi,
            &uf('+7W1011#'
            f(rsum((if p(v910) then 
                       if &uf('Ag991^t#1')<>'' and p(v910^t) and v910^t:&unifor('Ag991^t#1') or 
                          &uf('Ag991^t#1')='' and &uf('Ag1001^o#1')<>'' and 
                          p(v910^t) and v910^t:&unifor('Ag1001^o#1')
                       then 
                          if v910^a:'U' then f(val(v910^1,|1|n910^1)*val(v910^=,|1|n910^=),0,0)';' else 
                             if 'C 6 2 7':v910^a then else f(val(v910^=,|1|n910^=),0,0)';' 
                          fi fi 
                       else  
                          if s(&uf('Ag991^t#1'),&uf('Ag1001^o#1'))='' then  
                             if a(v910^t) then 
                                if v910^a:'U' then 
                                   f(val(v910^1,|1|n910^1)*val(v910^=,|1|n910^=),0,0)';' 
                                else 
                                   if 'C 6 2 7':v910^a then else f(val(v910^=,|1|n910^=),0,0)';' 
                    fi fi fi fi fi fi)),0,0)),
            if g1001^o<>'' then 
               &uf('+7W10111#'
               f(rsum((if p(v910) then 
                          if a(v910^t) then  
                             if v910^a:'U' then f(val(v910^1,|1|n910^1)*val(v910^=,|1|n910^=),0,0)';' 
                             else 
                                if 'C 6 2 7':v910^a then else f(val(v910^=,|1|n910^=),0,0)';' 
                       fi fi fi fi)),0,0)),
            fi,
            &uf('+7W20#',f(rsum((if p(v910) then if &uf('Pv910^t')<>'' then '1;' fi fi)),0,0)),
            if val(&unifor('IMAIN,ACCESSRDR,1'))=1 then 
               &unifor('+7W1111#',(if p(v691) then 
                                      if p(v691^0) then 
                                         &unifor('7RDR,!UPS='v691^0,'!,mfn/') 
                                         &unifor('7RDR,!?',v691^0,'!,mfn/') 
                                         &unifor('7RDR,!',&unifor('G2*'v691^0),'!,mfn/') 
                                   fi fi)),&uf('+7G1111'),
               &uf('+7W1013#'f(rsum((if p(g1111) then '1;'fi/)),0,0))                      
            else 
               &uf('+7W1013#'f(rsum((if p(v691) then 
                                        if p(v691^0) then 
                                           &unifor('DVUZ,"!',v691^0,'",v68^Z')';' 
                                     fi fi)),0,0)), 
            fi,
            if val(&unifor('IMAIN,ACCESSRDR,1'))=1 then 
               &unifor('+7W1112#',
                  (if p(v691) then 
                      if p(v691^0) then 
                         if &uf('Ag991^o#1')='' and &uf('Ag1001^o#1')='' or 
                            &uf('Ag991^o#1')<>'' and &uf('+97',v691^o)=&uf('+97',&uf('Ag991^o#1')) or 
                            &uf('Ag1001^o#1')<>''and &uf('+97',v691^o)=&uf('+97',&uf('Ag1001^o#1')) 
                         then 
                            &unifor('7RDR,!UPS='v691^0,'!,mfn/') 
                            &unifor('7RDR,!?',v691^0,'!,mfn/') 
                            &unifor('7RDR,!',&unifor('G2*'v691^0),'!,mfn/') 
                   fi fi fi)),&uf('+7G1112'),
               &uf('+7W10131#'f(rsum((if p(g1112) then '1;'fi/)),0,0))                      
            else 
               &uf('+7W10131#'f(rsum(
                  (if p(v691) then 
                      if p(v691^0) then 
                         if s(&uf('Ag991^o#1'),&uf('Ag1001^o#1'))='' or 
                            &uf('Ag991^o#1')<>'' and &uf('+97',v691^o)=&uf('+97',&uf('Ag991^o#1')) or 
                            &uf('Ag1001^o#1')<>''and &uf('+97',v691^o)=&uf('+97',&uf('Ag1001^o#1'))
                         then 
                            &unifor('DVUZ,"!',v691^0,'",v68^Z')';' 
                   fi fi fi)),0,0)), 
            fi,
      '^A',briefkt,'^D'
      if p(v951) then f(val('100'),0,2)
      else 
         if val(g10)= 0 then '0'
         else
         f(val(g1011)/val(g10131) + 
           val(if val(g1013)>0 and val(g20)>1 then g10111 fi)/ val(g1013),0,2)
      fi fi,
      '^C'if p(g1013) then g1013 else '0' fi,
      '^B'if p(g1012) then g1012 else '0' fi,
      '^E'if p(g10131) then g10131 else '0' fi,|^!|g1001^o,/


