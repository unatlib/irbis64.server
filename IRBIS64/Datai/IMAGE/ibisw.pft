'{\pard\tx4',mhl,
   if v907^C:'ОБРНЗ' then '\qc\b Издание в обработке. Не выдается\par \b0 \ql' fi,
   osvex,
   if v900^c:'34'then'\b Препринт\b0 \par 'fi'\b '
   orchifr,
   |. |+v981^a".\par ",v503^a".\par ",'\b 'v461^x,v461^b,
   if p(v461) then 
      if v461^x:','or p(v461^b) then'.'fi,
      if s(v461^x,v461^b)=''and s(v961^z,v962^z)<>''then 
         (if p(v961^z) then v961^a,| |v961^d,|, |v961^g,|. |d461^c,
             if a(v961^g) then| |v961^b,| |d461^c fi 
         fi), 
         (if p(v962^z) then|. |+d962^z,v962^a,
             if s(v962^n,v962^c)<>''then' ('v962^n|; |,v962^c')'fi,|. |v962^b,
             if s(v962^d,v962^f,v962^e)<>''then' ('v962^d|; |,v962^f,
                if p(v962^f)then|; |v962^e else v962^e fi,|/ |v962^h,|/ |v962^i,')'
         fi fi)  
      fi,
      if s(v461^x,v461^b,v961^z,v962^z)<>''then'\b0 \par \tab ',v461^c, 
      else'  '&unifor('E3'v461^c)'\b0 ',
         if &unifor('F3'v461^c)<>''then' '&unifor('F3'v461^c) fi,
      fi,
      if s(v230v135)<>''then 
         if &unifor('Kjzk.mnu|'&unifor('Av101#1'))<>''then' [Электронный ресурс]' 
         else' [Electronic resource]'
      fi fi,| = |v46^l,|: |v461^e,
      &unifor('6o461f'),|. - |v461^p,". - "d230^a,
      (if p(v230^a) then 
          if &unifor('Kjzk.mnu|'&unifor('Av101#1'))<>''then""| и |+v230^a 
          else ""| and |+v230^a 
      fi fi),
      (if s(v982^1,v982^4,v982^5)<>''then'. - 'v982^1|; |,|№ ГР |v982^4,|; Инв.№ |v982^5 fi),
      &unifor('6ovixdM'),
      (|. - (|v46^a,|; |v46^v,if s(v46^a,v46^v)<>''then|)|d46 fi,|. - |v46^t*4,|: |v46^r,|. - |v46^p),
      if s(v454^u)<>'' then &unifor('6origs454') fi ,|. - \b Пер.загл.:\b0  |v461^a,
      if s(v337v230^d)<>''then'\par    '
         (if p(v230) then if v230^x<>'0'then"\b Систем. требования:\b0  "|. |+v230^d,
             | (|v230^e|)| fi fi),
         (|. - Режим доступа: |v337^d,|. - |v337^e,|. - |v337^f,|. - |v337^g,
          |. - |v337^h,v337^a) 
      fi,
      if v10^a:v461^i or s(v10^a)=''then else|. - \b ISBN \b0 |v461^i fi,
      |. - ISSN |v461^j,'\par '
      "\tab \b "v200^v"\b0 ",
      if p(v200^v) and p(v200^a) then': 'fi,
   fi,
   if p(v200^a) then &unifor('6ozbo') fi,
   if p(v461) then v200^a 
   else 
      if s(v700v710)=''then'\tab \b '&unifor('E3'v200^a),'\b0 ',
         if &unifor('F3'v200^a)<>'' then' '&unifor('F3'v200^a) fi 
      else "\par \tab "v200^a 
   fi fi,
   if a(v461) then 
      if s(v230v135)<>''then 
         if &unifor('Kjzk.mnu|'&unifor('Av101#1'))<>''then' [Электронный ресурс]' 
         else' [Electronic resource]'
   fi fi fi,
   if p(v922) then &unifor('6ibis922') fi,
   if s(v510^e)<>''then|: |v200^e,
      (if p(v923) then", "|; |+d923,v923^h,
          if p(v923^h)then|. |d923^i fi,v923^i,
          if s(v923^k,v923^l)<>''then'. 'fi,v923^k,
          if p(v923^k)then|. |d923^l fi,v923^l
      fi) 
   fi,
   if s(v510^f)<>''then|/ |v200^f,|; |v200^g fi,
   (| = |v510^d,|: |v510^e,|/ |v510^f,|; |v510^g),(| = |v921^a,| (|v921^b|)|),|: |v924,
   if s(v510^e)=''then 
      if a(v922) then|: |v200^e,
         if a(v200^e) and p(v328^a) then 
            if s(v900^c,v900^2,v900^3,v900^4):'m1'then': Автореферат дис. ... 'else 
               if s(v900^c,v900^2,v900^3,v900^4):'m2' then': Дис. ... '
            fi fi,v328^a,": "v328^n,", "v328^0,", "v328^9 
      fi fi,
      (if p(v923) then ", "|; |+d923,v923^h,
          if p(v923^h)then|. |d923^i fi,v923^i,
          if s(v923^k,v923^l)<>''then'. 'fi,v923^k,
          if p(v923^k)then|. |d923^l fi,v923^l
      fi)
   fi,
   if s(v510^f)=''then 
      if s(v921v924)<>''or (p(v510)and s(v510^e)=''or a(v510))and s(v923v200^e)<>''then|/ |d200^f 
      else 
         if &unifor('Av922^f#',f(rsum((|1;|d922)),0,0))<>'' or 
            &unifor('Av922^g#',f(rsum((|1;|d922)),0,0))<>'' 
         then|; |d200^f 
         else |/ |d200^f 
      fi fi,v200^f,"; "v200^g,
      if a(v200^f) then &unifor('+1W1#0'),&unifor('+1W1#',&unifor('6o200f')),
                        &unifor('+1W2#0'),&unifor('+1W2#',&unifor('6o200g')),
         if &unifor('+1R1')<>'' or &unifor('+1R2')<>'' then'/' 
            if s(v700v701v328)<>''then &unifor('+1R1'),if &unifor('+1R2')<>''then'; '&unifor('+1R2') fi
            else &unifor('+1R2'),
               if &unifor('+1R1')<>''then if &unifor('+1R2')<>''then'; 'fi,&unifor('+1R1')
   fi fi fi fi fi,
   if p(v925) then &unifor('6ibis925') fi,". - "v205^a,", "v205^b,
   if s(v205^a,v205^b)<>''then"/ "v205^f else". - "v205^f fi,
   if a(v461) then". - "d230^a,
      (if p(v230^a) then 
          if &unifor('Kjzk.mnu|'&unifor('Av101#1'))<>''then""| и |+v230^a 
          else ""| and |+v230^a fi,
       fi) 
   fi,
   &unifor('6ospec'),
   if a(v461) then 
      (if s(v982^1,v982^4,v982^5)<>''then'. - 'v982^1|; |,|№ ГР |v982^4,|; Инв.№ |v982^5 fi)
   fi,
   ovixd,
   okolhr,
   if p(v115) or p(v123) or p(v125) or p(v126) or p(v130) or p(v135) or p(v230) or p(v915) or p(v916) 
   then &unifor('6ofizxar0') fi,
   (| + |v215^e,| |d215^2,&unifor('G2='v215^2)),
   if p(v115) or p(v123) or p(v125) or p(v126) or p(v130) or p(v135) or p(v230) or p(v915) or p(v916) 
   then &unifor('6ofizxar1') fi,
   if p(v225) then &unifor('6oser') fi,
   (if v517^n:'0'then else
       if p(v517) then'. - ',
          if v517^t.4='540 'then |[|v517^a|]| else v517^t*4,|: |v517^a 
   fi fi fi),
   if v920.1:'A'then else if p(v454) then &unifor('6origp454')fi fi,
   oistasp,
   oISBNcn,
   if s(v463)<>''then else
      if s(v905^m)=''and &unifor('IPRIVATE,FPP,')=''then "\par  "|. |+v300 fi 
   fi,
   "\par \b   Перевод заглавия: \b0  "v541,
   if p(v391)then" \par \b Есть автограф: \b0 "d391,
      (if p(v391)then v391^a,| |v391^b,| (|v391^c|)|,v391.1+|; | fi) fi,
   "\par \b Экслибрис: \b0 "|; |+v398,"\par \b Пометки автора: \b0 "|; |+v395,
   "\par \b Пометки владельца коллекции: \b0 "|; |+v396,
   "\par \b Сохранность документа: \b0 "|; |+v399,
   if p(v140) or p(v141) or p(v316) or p(v317) or p(v318) or p(v712) or p(v116) then &unifor('6oant') fi,
   if p(v421) or p(v422) then &unifor('6opril421') fi,
   if val(&unifor('J,I470='v903))>0 then '\par ' &unifor('+I?2,,,,I470='v903'?Есть рецензия/реферат') fi,
   "\par  "v327,
   if p(v451) or p(v452) then &unifor('6opri451') fi,
   if p(v330) then &unifor('6o330') fi,
   if p(v675) or p(v621) or p(v964) or p(v686) then &unifor('6ogrudk') fi,
   if p(v606) or p(v607) or p(v610) or p(v965) or s(v330^5,v922^5)<>'' then &unifor('6orubdks') fi,
   if s(v905^h)<>''or &unifor('IPRIVATE,FPA,')<>''then else ("\par \b Аннотация: \b0 "|\par    |+v331"\par ") fi,
   if p(v481) then &unifor('6oprip481') fi,
   odoptd,
   if p(v910) or v920.1:'A'then &unifor('6oex910') fi,
   if p(v951) then'\par  см. ссылк',
      if rsum((if s(v951^i,v951^a)<>''then'1;'else'0;'fi))>1 then'и:   'else'у 'fi,
      (if p(v951^a) then |,  |+d951,' ',v951^t,| ФАЙЛ|n951^t fi),
      (if p(v951^i) then |,  |+d951, &unifor('+I?0,,'v951^i'?',v951^t,| INTERNET|n951^t) fi) 
   fi,
   "\b Учебная литература: \b0 \par "d691,
   (v691^a,|/|v691^b,|/|v691^c,|/|v691^d,|/|v691^e,|/|v691^f,|/|v691^g,|\par |d691),
   ("\b Держатели документа: \b0 \par "v902^a,|: |v902^b,|\par |d902),
   '\par ----------------------------------------\par \pard\tx4 }',&unifor('!')
