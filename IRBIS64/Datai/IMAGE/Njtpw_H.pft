'</>',
if v910:'^c'then 
   if v910:'^a0'or rsum((if v910^a:'U'then 
            if p(v910^2) and val(v910^1,|1|n910^1)-val(v910^2)>0 or a(v910^2)then'1;' 
            fi fi,if v910^a:'p'then ref(l(|I=|v910^p),if v910^a:'0'then'1;'else"0;"d910 
                  fi) fi'<br> '))>0 then  
   else '</dd> (Свободных экземпляров нет) <br> '   
   fi 
else '<br><dd> (Нет сведений об экземплярах) <br> ' 
fi, 
'<b> Шифр: 'v903, 
ref(l(|I=|v933),'<br><dd> ', &unifor('K110b.mnu|'v110^b)), 
if p(v904^c) then'<br> (',&unifor('G2='v904^c),' - 'v904^a,')'fi,   '</b> ', 
ref(l(|I=|v933),"<br> "d200,&unifor('6ozboz_H'), 
   if p(v200^e) then ' : ' 
      if s(v982^0,v982^9)<>'' or v905^2:'1' or &unifor('Q'v200^e*1.1)<>v200^e*1.1 then v200^e else 
         &unifor('Q'v200^e.1),v200^e*1 
   fi fi,
(". "| ; |+v923^h,if v923^h:':' then else if p(v923^h)then |, |d923^i else |. |d923^i fi,fi,v923^i,|. |v923^k,if v923^k:':' then else if p(v923^k)then |, |d923^l else |. |d923^l fi fi,v923^l,|. |v923^m,if v923^m:':' then else if p(v923^m)then |, |d923^n else |. |d923^n fi fi,v923^n)
,"/ "v200^f), 
if s(v230,v215)=''then 
   ref(l(|I=|v933),". - "d230^a, 
  (if p(v230^a) then if &unifor('Kjzk.mnu|'ref(l("I="v933),v101.3))<>''then""| и |+v230^a else ""| and |+v230^a fi fi)) 
fi, 
ref(l(|I=|v933),(". - "| ; |+v210^a,| ; |v210^x,| ; |v210^y,| : |v210^c,), 
   if s(v210^a,v210^c)<>''then", "v210^d" - " else". - "v210^d fi,if p(v210^d) then" - "v210^e,"    "n210^e fi), 
if s(v230,v215)=''then 
   ref(l(|I=|v933), 
   if s(v215,&unifor('G2='v230^b),&unifor('G2='v230^c))<>''then'. - 'fi,v215^a," "d215^1,&unifor('G2='v215^1), 
   if a(v215^1)then" c."d215^a fi,if p(v215^a)then" : "d215^c fi,v215^c,if s(v215^a,v215^c)<>''then" ; "d215^d fi,v215^d, 
   (if s(&unifor('G2='v230^b),&unifor('G2='v230^c))<>''and p(v215)then'<b> : 'fi,&unifor('G2='v230^b), 
    if &unifor('G2='v230^c)<>''then if &unifor('G2='v230^b)<>''then', 'fi,&unifor('G2='v230^c)'</b> 'fi)) 
fi, 
ref(l(|I=|v933), 
   (if v115^x:'0'then| (|d115^b,f(val(v115^b),0,0),| мин.)|d115^b,| : |v115^c*3,|, |v115^d*3,|; |v115^e*3,|, |v115^f*3, 
    |, |v115^h*3,|, <b> |v115^j*3|</b> |,|, |v115^k*3,|, |v115^l*2 fi), 
   (if v915^x:'0'then| (|d915^b,f(val(v915^b),0,0), 
       if v915^g.1:'k'then' дп.'else 
          if v915^g.1:'l'then ' трансп.'else ' кадр.'fi fi, 
       |)|d915^b,' : 'v915^c*3,|, |v915^f*3,|, <b> |v915^g*3|</b> |,|, |v915^p*3,|, |v915^q*3 fi), 
   (if v916^x:'0'then| (|d916^b,f(val(v916^b),0,0)| мин.)|d916^b,' : 'v916^1| кадр/с.|,|, |v916^c*3,|, |v916^d*3,|, |v916^e*3, 
    |; |v916^2| м|,|, |v916^f*3,|, <b> |v916^g*3|</b> |,|, |v916^h*3,|, |v916^i*3,|, |v916^m*3,|, |v916^n*2 fi), 
   if a(v230) then(if v135^x:'0'then| : <b> |v135^a*3|</b> |,|, |v135^b,|, |v135^c,|, |v135^d fi) fi, 
   (if v130^x:'0'then| : <b> |v130^a*3|</b> |,|, |v130^b*3,|(|v130^c*3|)|,|, |v130^d*3,|, |v130^e,|, |v130^f*3,|, |v130^g*3, 
    |, |v130^h*3,|, |v130^i*3 fi), 
   (if v125^x:'0'then| (|d125^a, 
       if v125^a.2:'00'then else if v125^a.1:'0'then v125^a*1.1| час.|else v125^a.2| час.|fi fi, 
       if v125^a*2.2:'00'then else if v125^a*2.1:'0'then v125^a*3.1| мин.| else v125^a*2.2| мин.| fi fi, 
       if v125^a*4.2:'00'then else if v125^a*4.1:'0'then v125^a*5.1| сек.|else v125^a*4.2| сек.|fi fi,|)|d125^a, 
       |, |v125^b*3,|, |v125^c*3,| |v125^d*2,|, |v125^e*5,|, |v125^1*2,| |v125^2,|, |v125^3*2,| |v125^4 fi), 
   if v125^x:'0'and v126^x:'0'then if p(v126^a) then else ', 'fi fi, 
   (if v126^x:'0'then |<b> |v126^a*3|</b> |,|, |v126^b*3,| (|d126^1,f(val(v126^1),0,0),| мин.)|d126^1,|, |v126^c*3,|, |v126^d*3, 
    |, |v126^e*3,|, |v126^f*3,|, |v126^g*3,|, |v126^h*3,|, |v126^i*3,|, |v126^j*3 fi), 
   (if v123^x:'0' then | : <b> |v123^a*3|</b> |,|, |v123^b*3 fi), 
   " +"v215^e,| |d215^2,&unifor('G2='v215^2),"  "v215^3,", "v215^x" экз."), 
if s(v230v215)=''then 
   ref(l(|I=|v933), 
   if s(v337v230^d)<>''then". - <b> Систем. требования:</b>  "d230^d, 
      (if p(v230) then if v230^x:'1'then'; Прил.: 'else|. |+d230^d fi,v230^d,| (|v230^e|)|fi), 
      (|. - Режим доступа: |v951^i,| (|v951^t|)|,|. - |v337^e,|. - |v337^f,|. - |v337^g,|. - |v337^h,|. - |v337^a) fi) 
fi, 
ref(l(|I=|v933),". - ISSN "v11^a,". - "d110^d, 
  if &unifor('Krksp.mnu|'v110^d)<>''then &unifor('Krksp.mnu|'v110^d) else"Периодичность "v110^d fi, 
  ". - Издание является приложением к "v422^a," ("v422^v")",", ISSN "v422^x,), 
"<br>    <b> "v934"г.", 
if p(v935) then 
   if val(v935.1)=0 and (not(v935.1='0')) then' ' 
   else ref(l("I="v933),if p(v210^8) then' 'v210^8' 'else' т.'fi) 
   fi,v935 fi , 
if p(v936) then 
   if val(v936.1)=0 and (not(v936.1='0'))then' ' 
   else ref(l("I="v933),if p(v210^9) then' 'v210^9' 'else' N 'fi) 
   fi,v936 fi, 
| (|v931^c|)|,"</b> "d934, 
(if p(v930)then if v930^a<>''then |<br>    <b> |d930, 
    if v930^0<>''then v930^0'г.', 
       if v930^t<>''then ref(l("I="v933),if p(v210^8) then' 'v210^8' 'else' т. 'fi),v930^t,ref(l("I="v933),if p(v210^9) then' 'v210^9' 'else' № 'fi,), &unifor('G2/',&unifor('G2/',&unifor('G2/'v930^a))) 
       else' N ',&unifor('G2/',&unifor('G2/'v930^a)) fi fi fi fi/),"</b> "d930, 
if s(v230v215)<>''then ". - "d230^a, 
   (if p(v230^a) then if &unifor('Kjzk.mnu|'ref(l("I="v933),v101.3))<>''then""| и |+v230^a else ""| and |+v230^a fi fi), 
   (if s(v215,&unifor('G2='v230^b),&unifor('G2='v230^c))<>''then'. - 'fi,v215^a," "d215^1,&unifor('G2='v215^1)), 
   (if s(&unifor('G2='v230^b),&unifor('G2='v230^c))<>''and p(v215)then'<b> : 'fi,&unifor('G2='v230^b), 
   if &unifor('G2='v230^c)<>''then 
      if &unifor('G2='v230^b)<>''then', 'fi,&unifor('G2='v230^c)'</b> ' 
   fi), 
   (| + |v215^e,| |d215^2,&unifor('G2='v215^2)), 
   if s(v337v230^d)<>''then". - <b> Систем. требования:</b>  "d230^d, 
      (if p(v230) then 
          if v230^x:'1'then|; Прил.: |d230^d else|. |+d230^d fi,v230^d,| (|v230^e|)|fi), 
      (|. - Режим доступа: |v951^i,| {|v951^t|)|,|. - |v337^e,|. - |v337^f,|. - |v337^g,|. - |v337^h,|. - |v337^a) 
   else 
      ref(l("I="v933), 
      if s(v337v230^d)<>''then". - <b> Систем. требования:</b>  "d230^d, 
         (if p(v230) then if v230^x:'1'then|; Прил.: |d230^d else|. |+d230^d fi,v230^d,| (|v230^e|)|fi), 
         (|. - Режим доступа: |v951^i,| {|v951^t|)|,|. - |v337^e,|. - |v337^f,|. - |v337^g,|. - |v337^h,|. - |v337^a) 
      fi), 
fi fi, 
". - "v931^b," (Есть прилож.: "d931^e,(| ; |+d931^e,if v931^e*1.1='='then v931^e*2 else v931^e fi,| - Шифр |v931^d),")"d931^e, 
". - "v904^b,if p(v904^c) then' (',&unifor('G2='v904^c),')'fi,". - "v320, 
if rsum((v910^e|;|))>0then '. - ', 
   (if p(v910)then 
       if &unifor('Pv910^e')<>''then &unifor('Pv910^e'),', 'fi, 
    fi),&unifor('Av910^l#1'), 
    if a(v910^l)then &unifor('Korg.mnu|3') fi 
fi, 
"<b> <br> В номере также: <br> </b> "d423,(| ; |+d423,v423^a,| N |v423^n,|, шифр: |v423^h,), 
if p(v922)then &unifor('6o922nj_H')fi, 
"<br> <b> На вкладках: </b> "d330,(| ; |+d330,v330^f| |,v330^c,| (|v330^p|)|), 
if v910:'^c'then'<br> <b> Имеются экземпляры в отделах: </b> '   
   if &unifor('IPRIVATE,FPI,')<>''then 
      (if p(v910^c)then'<br> 'v910^d,|(в подшивке)|d910^p,' ('v910^c*6.2'.'v910^c*4.2'.'v910^c.4'г.', 
            |(|v910^1|экз.)|,| Экз. |v910^b,' - ',v910^e, 
          if val(v910^e)>0 then 
             if &unifor('Av10^c#1')<>''then &unifor('Av10^c#1') 
             else &unifor('Korg.mnu|3') 
          fi fi 
          if a(v910^e)then &unifor('Av10^d#1'), 
             if val(&unifor('Av10^d#1'))>0 then 
                if &unifor('Av10^c#1')<>''then &unifor('Av10^c#1') 
                else &unifor('Korg.mnu|3') 
             fi fi 
             if &unifor('Av10^d#1')='' then 'Б.ц.'fi 
          fi,|)|d910^c, 
       fi), 
   else 
       if rsum((|1;|d910^c))>1 then '</dd> всего  ' 
          f(rsum((if p(v910^c) then 
                     if'C U':v910^a then v910^1|;|,|1;|n910^1 else 
                        if '6 2 7':v910^a then else'1;' fi 
                     fi 
                  fi)),0,0),': ', 
       fi,&unifor('O') 
   fi, 
   if &unifor('Y')<>''then'<br> Свободны: ',&unifor('Y') fi,'<br> ' 
fi, 
if rsum((if'CU':v910^a then if p(v910^c)then'0;'else'1;'fi else'0;'fi))>0 
or v910^a:'2' 
then'<br> Заказаны экземпляры для отделов: ' 
  (if v910^a:'2'or 'CU':v910^a and a(v910^c) then v910^d,|(|v910^1|)|,', 'fi) 
fi, 
(if v910^a:'p'then|<br> Экз.|v910^b,|(|v910^d|)|,' переплетен ',|(см. |v910^p|)|, 
    ref(l(|I=|v910^p),if v910^a:'0'then' - cвободен'else' - занят 'fi) fi), 
(if v910^a:'3'then|<br> Экз.|v910^b,|(|v910^d|)|,' в переплете'fi), 
(if v910^a:'4'then|<br> Экз.|v910^b,|(|v910^d|)|,' утерян'fi), 
(if v910^a:'5'then|<br> Экз.|v910^b,|(|v910^d|)|,' временно не выдается' fi), 
(if v910^a:'8'then|<br> Экз.|v910^b,|(|v910^d|)|,' поступил, но еще не дошел до места хранения'fi),
if p(v937)then'<br> <b> Технологический путь: </b> ',
   '<br> N экз. Куда ПЕРЕДАЧА  фио ВОЗВРАТ  фио','<br> '
'----------------------------------------------------------'
(if p(v937)then |<br> |v937^n,| |v937^a,| |v937^b*6,|.|v937^b*4.2,|.|v9
37^b.4,| |v937^c,| |v937^d*6,|.|v937^d*4.2,|.|v937^d.4,| |v937^e fi)
fi,
if p(v940) or s(v910^v)<>''then '<br> <b> Списание экземпляров:</b> <br> '
   (if v910^a:'6'and p(v910^v) and a(v910^w) then
       | Инв.|v910^b| списан из |,v910^d,' ; N акта списания 'v910^v'<br> 'fi),
   (if v910^a:'6'and p(v910^v) and p(v910^w) then 
       | Инв.|v910^b,| передан в |v910^m,| из |v910^d,'; N акта передачи 'v910^w'<br> '
   fi),
   (if 'C U':v940^a and p(v940^x) then| |v940^x|экз. списаны из |,v940^d,
       ' ('v940^b,'; N акта списания 'v940^v,')','<br> ' fi),
   (if 'C U':v940^a and p(v940^w) then | |v940^z,| 1|n940^z,' экз. ',' ('v940^b,')',
       if val(v940^z)<=1 then | был передан в |v940^m else | было передано в |v940^m        fi,| из |v940^d,'; N акта передачи 'v940^w '<br> '
   fi)
   (if 'C U':v910^a and p(v910^v) and p(v910^x) then
       | |v910^x|экз. списываются из |,v910^d,
       ' ('v910^b,'; N акта списания 'v910^v,')','<br> ' fi),
   (if 'C U':v910^a and p(v910^v) and p(v910^w) then 
       | |v910^z,|1|n910^z,' экз. ',' ('v910^b,')',
       if val(v910^z)<=1 then | передается в |v910^m 
       else | передаются в |v910^m fi,| из |v910^d,'; N акта передачи 'v910^w '<br> '
   fi)
fi,

''
 
&unifor('!'), 
