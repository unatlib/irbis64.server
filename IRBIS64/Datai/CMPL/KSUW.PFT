'{', 
if v920: 'RSU' 
then 
   'Сетевой удаленный ресурс: \b ',,v881^T,,
   "\par \b0 Исполнитель: \b "v881^D,,,
   "\par \b0 Контракт: \b "d882,,,
   (if p(v882) then 
       |№ - \b |v882^N,,,|\b0  от \b |v882^D*6.2,'.',v882^D*4.2,'.',v882^D.4,,,if s(v881^I,v881^E)<>'' then '.  ' fi,,"\b0 Пакетов: \b "v881^I,,"\b0  Док-ов: \b "v881^E,,
       '\par \b0 Срок действия \b ',,| от |d882^E,,v882^E*6.2,'.',v882^D*4.2,'.',v882^E.4,,
       | до |d882^F,,,v882^F*6.2,'.',v882^F*4.2,'.',v882^F.4,,
    fi/),,,
fi,,,,,,,,,,,,,,,,,,,
if v920:'KSU' or v920:'KSF' or v920:'KSI' 
then 
   '\b ЗАПИСЬ КНИГИ СУМ. УЧЕТА ' 
   if v920:'KSI' or 
      v920:'KSF' or
      p(v88^a) and a(v88^b) 
   then
/*--------------------------------итоговая КСУ
       if v920:'KSI' then '(итоговая на ',v88^B,,') \b0 ' fi,
       if v920: 'KSFP' then '(поступление в фонд за ',v88^B,,' )\b0 ' fi,,,,
       '\par \b Источник комплектования: \b0 ' 
       if l('kor='v88^d)<1 then v88^d else ref(l('kor='v88^d),v89^a,if a(v89^a)then v81^a fi)fi, 
/*очагова
       '\par Всего - наименований \b ',,f(val(v145^G)+val(v155^V),0,0),'\b0 ',,
       '\par Всего - экземпляров \b ',f(val(v145^B)+val(v145^E)+val(v145^V),0,0),,,'\b0 ',, 
       '\par На сумму  ',,,v145^I,' ',&unifor('Korg.mnu|3'),
       "\par    Наименований,поступивших впервые (без периодики) на баланс: "v17^1,
        "\par    Экз-ров (без периодики) на баланс - всего: "v145^b, 
        "\par    Сумма (без периодики) на баланс: "v145^c, 
        "\par    Наименований,поступивших впервые (без периодики) НЕ на баланс: "v17^b, 
        "\par    Экз-ров (без периодики) НЕ на баланс - всего: "v145^e, 
        "\par    Сумма (без периодики) НЕ на баланс: "v145^f, 
        "\par    Наименований,поступивших впервые ВСЕГО (без периодики): "v17^a, 
        "\par    Экз-ров ВСЕГО: "v145^h, 
        "\par    Сумма ВСЕГО: "v145^i, 
        "\par    КНИГИ (наименований,поступивших впервые): "v18^2, 
        "\par    КНИГИ (экз): "v145^k, 
        "\par      в т.ч. Моноиздания(наименований,поступивших впервые): "v18^3, 
        "\par      в т.ч. Моноиздания(экз): "v145^q, 
        "\par      в т.ч. МН - число томов,поступивших впервые: "v18^5, 
        "\par      в т.ч. МН (экз): "v145^m, 
        "\par      в т.ч. Продолж. изд. - число выпусков: "v145^n, 
        "\par      в т.ч. Продолж. изд.(экз): "v145^o, 
        "\par      в т.ч. Серии - число выпусков: "v145^r, 
        "\par      в т.ч. Серии (экз): "v145^s, 
        "\par    Брошюры (наименований,поступивших впервые): "v18^4, 
        "\par    Брошюры (экз): "v145^u, 
        "\par    Журналы (наименований): "v155^v, 
        "\par    Журналы (экз): "v145^v, 
        "\par    Газеты (наименований): "v155^x, 
        "\par    Газеты (экз): "v145^x, 
        "\par    Отечественные книги (наименований,поступивших впервые): "v18^6, 
        "\par    Иностранные книги (наименований,поступивших впервые): "v18^7, 
        "\par    Отечественные журналы (наименований): "v155^a, 
        "\par    Иностранная периодика (наименований): "v155^c, 
        "\par      в т.ч. журналы (наименований): "v155^q, 
        "\par      в т.ч. газеты (наименований): "v155^r, 
        '\par ---------------------------------------------', 
        '\par \b РАСПРЕДЕЛЕНИЕ ДОКУМЕНТОВ \b0 ' 
   else '(ч.1)\b0 ','\par -----------------------\par ', 
/*--------------------------------------обычная КСУ
        '\b Год/ номер записи: \b0 '&unifor('Av88^a#1'), 
        '\par \b Источник комплектования: \b0 ' 
        if l('kor='v88^d)<1 then v88^d else ref(l('kor='v88^d),v89^a,if a(v89^a)then v81^a fi)fi, 
        '\par \b ПОСТУПИЛО ВСЕГО (по сопроводительному документу № 'v88^c,'): \b0 ', 
        (if v88<>'' 
         then 
            if p(v88^a) 
            then 
               |\par  Номер акта |v88^y,'\par Дата ',v88^b*6'.',v88^b*4.2'.', 
               v88^b.4,'\par Наименований 'v88^e'\par Экземпляров 'v88^f, 
               '\par На сумму ',v88^g,&unifor('Korg.mnu|3') 
               if s(v88^n,v88^p)<>''then'\par    (в том числе: ', |НДС-|v88^n,| Почт.расходы-|v88^p,')' fi 
            fi 
         fi),
            '\par ------------------------------------------------------', 
             '\par \b Анализ партии\b0 ' 
             '\par Всего - наименований 'f(val(v45^4)+val(v155^v),0,0),,
             '\par Всего - экземпляров 'v45^5,,,
             '\par На сумму  'v145^i,&unifor('Korg.mnu|3')," ( из них"d145^i if val(v145^i)-val(v910^2)<>0 then ' не на баланс 'f(val(v145^i)-val(v910^2),0,2),' ' &unifor('Korg.mnu|3'),"; "d910^2 fi," на баланс "v910^2, if p(v910^2)then ' ' &unifor('Korg.mnu|3') fi, ")"d145^i, 

            "\par Число наименований, поступивших в библиотеку впервые (1-ые экземпляры): "v17^a, 
            "\par Число наименований, поступивших от данного источника повторно: "v19, 
            if v910^1<>''then'\par Минимальный инвентарный номер в партии ' fi,
            if &unifor('Av910^1#2')<>''then'\par 'fi,
            v910^1| / |, 
            if rsum((if p(v910) then if v910.1:'^'then '0;'else '1;' fi fi),)>0 
            then 
               '\par Максимальный инвентарный номер в партии ' 
            fi,
            if &unifor('Av910^1#2')<>''then'\par 'fi, 
            (if p(v910) then if v910.1:'^'then else v910| / | fi fi), 
      if f(val(v145^i,' 0'),0,2)= 
         f(val(&unifor('Av88^g#1')) - rsum(&unifor('Av88^n#1')'; '&unifor('Av88^p#1')),0,2) 
      then else 
        '\par \b ВНИМАНИЕ! \b0 Вычисленная сумма (',v145^i, 
        ') не равна сумме, введенной с сопроводительного документа (',&unifor('Av88^g#1'), 
         if s(&unifor('Av88^n#1'),&unifor('Av88^p#1'))<>''then 
         ' за вычетом накладных расходов-'f(rsum(&unifor('Av88^n#1')'; '&unifor('Av88^p#1')),0,2), 
         '='   f(val(&unifor('Av88^g#1')) - rsum(&unifor('Av88^n#1')'; '&unifor('Av88^p#1')),0,2) 
         fi')', 
      fi, 
      if f(val(v45^4)+val(v155^v),0,0)=f(val(&unifor('Av88^e#1')),0,0) then else 
      '\par \par \b ВНИМАНИЕ! \b0 Вычисленное число наименований (',v45^4, 
      ') не равно числу, введенному с сопроводительного документа (', 
      f(val(&unifor('Av88^e#1')),0,0),')'fi, 
      if f(val(v45^5,' 0'),0,0)=f(val(&unifor('Av88^f#1')),0,0) then else 
      '\par \par \b ВНИМАНИЕ! \b0 Вычисленное число экземпляров (',v45^5, 
      ') не равно числу, введенному с сопроводительных документов (', 
      f(val(&unifor('Av88^f#1')),0,0),')'fi, 
      if &unifor('Av88#2')<>''then '\par \par \b ПОСТУПИЛО ПО КАЖДОМУ АКТУ: ', 
      '\b0 \par ------------------------------------------------------', 
      '\par \b N акта \tab \tab Наимен.\tab \tab Экз. \tab \tab На сумму \b0 ', 
         (if p(v88) and s(v88^E,v88^F) <>'' 
          then 
             if p(v88^y) then |\par |v88^y,'\tab \tab ',,,v88^e,'\tab \tab \tab ',,v88^f,'\tab \tab ',,v88^g,&unifor('Korg.mnu|3') 
         fi fi), 
         if v44^?<>'' then else 
            (if p(v44^i) then 
                if ref(mfn,(v88^y| |)):v44^i then 
                   if val(&unifor('JCMPL,?',v44^i'+'v44^k'+'v44^+))=1 then else 
                      '\par \b Внимание! \b0 Вычисленные данные для Акта № 'v44^i,| (|v44^k|экз.|, 
                      | на сумму |v44^+,|)|d44^k ' не совпадают с введенными' 
             fi fi fi) 
      fi fi 
      '\par ---------------------------------------------', 
      '\par \b РАСПРЕДЕЛЕНИЕ ПАРТИИ ДОКУМЕНТОВ \b0 ' 
         "\par    Отечественные книги (наименований): "v149^a, 
         "\par    Иностранные книги (наименований): "v149^c, 
         "\par    Отечественные журналы (наименований): "v155^a, 
         "\par    Иностранная периодика (наименований): "v155^c, 
         "\par      в т.ч. журналы (наименований): "v155^q, 
         "\par      в т.ч. газеты (наименований): "v155^r, 
   fi,
   if s(v145,v146,v147,v148,v155)<>'' then 
      if s(v145,v146)<>'' then '\par \b   Издания - по типам документов\b0 '
         "\par    Книги (экз): "v145^k, 
         "\par    Брошюры (экз): "v145^u, 
         "\par    Журналы (экз): "v145^v, 
         "\par       в т.ч. Информ. издания (экз): "v145^w, 
         "\par    Газеты (экз): "v145^x, 
         "\par    Диссертации (экз): "v146^a,
         "\par    Авторефераты (экз): "v146^b,
         "\par    Методические материалы (экз): "v148^g,
         "\par    НТД (всего) (экз): "v146^c,
         "\par       в т.ч. ГОСТ (экз): "v146^d,
         "\par       в т.ч. Пром.каталоги (экз): "v146^e,
         "\par       в т.ч. Патенты (экз): "v146^f,
         "\par       в т.ч. Нормативно-технические материалы (экз): "v146^g,
         "\par       в т.ч. Справочники (экз): "v146^h,
         "\par    Материал групповой обр-ки (экз): "v146^i,
         "\par    Неопубликованные док-ты (экз): "v146^j,
         "\par    Отчеты (экз): "v146^k,
         "\par       в т.ч. Отчеты о Загран.командировках (экз): "v146^l, 
         "\par    Картографический материал (экз): "v146^m,
         "\par    Ксерокопии (экз): "v146^n,
         "\par    Другие печатные изд. (экз): "v146^z,
      fi,
      if p(v147) then '\par \b   Нетрадиционные издания - по типам физ. носителей\b0 '
         "\par    Aудиовизуальные (единиц хранения): "v147^L, 
         "\par       в т.ч. КФ, диафильмы, диапозитивы (единиц хранения): "v147^j,
         "\par       в т.ч. Граммпластинки (единиц хранения): "v147^k,
         "\par    Электронные ресурсы (всего) (единиц хранения): "v147^c,
         "\par       в т.ч. Дискеты (единиц хранения): "v147^e,    
         "\par       в т.ч. CD-ROM (единиц хранения): "v147^f,
         "\par       в т.ч. Мультимедиа (единиц хранения): "v147^d,
         "\par    Микрофиши (единиц хранения): "v147^g,
         "\par    Микрофильмы (единиц хранения): "v147^h,
         "\par    Ноты (партитуры) (экз): "v147^i,
                  "\par    Другие непечатные изд. (экз): "v147^z,
      fi,
      if p(v148) then '\par \b   По характеру документа\b0 '
         "\par    Научные (всего)-(наименований): "v148^M,
         "\par    Научные (всего)-(экз): "v148^a,
         "\par       в т.ч. на нац.яз (экз): "v148^1,
         "\par    Учебные (всего)-(экз): "v148^c,
         "\par       в т.ч. на нац.яз (экз.): "v148^3,
         "\par    Художественная (экз): "v148^d,
         "\par       в т.ч. на нац.яз (экз): "v148^4,
         "\par    Общ-политическая (экз): "v148^e,
         "\par       в т.ч. на нац.яз (экз): "v148^5,
         "\par    Фин-экономическая (экз): "v148^l,
         "\par       в т.ч. на нац.яз (экз): "v148^?,
         "\par    Справочная лит-ра (наименований): "v148^N,
         "\par    Справочная лит-ра (экз): "v148^f,
         "\par       в т.ч. на нац.яз (экз): "v148^6,
         "\par    Официальные изд. (наименований): "v148^P, 
         "\par    Официальные изд. (экз.): "v148^H, 
         "\par       в т.ч. на нац.яз (экз): "v148^8,
         "\par    Энциклопедии (наименований): "v148^Q,
         "\par    Законы и законодательные акты (наименований): "v148^R, 
         "\par    Законы и законодательные акты (экз.): "v148^S, 
         "\par    Электронные учебники (наименований): "v148^T,
         "\par    Учебные - книги (наименований): "v148^b,
         "\par       в т.ч. книги на нац.яз : "v148^2,
         "\par    Учебные - журналы (наименований): "v155^b, 
         "\par       в т.ч. журн. на нац.яз.: "v155^2, 
         "\par    Журналы - Информ. издания (наименований): "v155^w, 
         if p(v148^U) then'\par    ' &uf('KHD.MNU!JJ'), ' - гриф JJ (экз.): 'v148^U fi,
         if p(v148^V) then'\par    '&uf('KHD.MNU!JJ1'),' - гриф JJ1 (экз.): 'v148^V fi,
         if p(v148^W) then'\par    '&uf('KHD.MNU!JJ2'),' - гриф JJ2 (экз.): 'v148^W fi,
         if p(v148^X) then'\par    '&uf('KHD.MNU!JJ3'),' - гриф JJ3 (экз.): 'v148^X fi,
         if p(v148^Y) then'\par    '&uf('KHD.MNU!JJ9'),' - гриф JJ9 (экз.): 'v148^Y fi,
      fi,
   else
      if p(v45)or p(v147)then
         if s(v45^a,v45^b,v45^c,v45^d,v45^e,v45^f,v45^g,v45^h,v147^a,v147^b)<>''then 
           '\par \b   По типам документов:\b0 ' 
            "\par книги:  "v45^a" экз.", 
            "\par брошюры: "v45^b" экз.", 
            "\par журналы: "v45^c" экз.", 
            "\par аудио: "v147^a" экз.", 
            "\par видео: "v147^b" экз.", 
            "\par аудиовизуальные: "v45^d" экз.", 
            "\par карты: "v45^e" экз.", 
            "\par ноты: "v45^f" экз.", 
            "\par комп.файлы: "v45^g" экз.", 
            "\par прочие: "v45^x" экз.", 
    fi fi fi, 
    if s(v45^1,v45^2,v45^3,v149^s,v149^u,v149^v,v149^1,v149^2,v149^3,v149^4,)<>''
    then'\par \b   По языку основного текста:\b0 ', 
       if s(v45^1,v149^s)<>'' then'\par на русском яз.: 'v149^s,if a(v149^s) then v45^1 fi,' экз.' fi, 
       if s(v45^2,v149^u)<>'' then'\par на национальном яз.: ',v149^u,if a(v149^u) then  v45^2 fi,' экз.'fi, 
       if s(v45^3,v149^v)<>'' then'\par на других (кроме русского и нац.) яз.: 'v149^v,if a(v149^v) then v45^3 fi,' экз.'fi, 
         "\par    в т.ч. на английском яз.: "v149^1" экз.",
         "\par    в т.ч. на французском яз.: "v149^2" экз.",
         "\par    в т.ч. на немецком яз.: "v149^3" экз.",
         "\par    в т.ч. на испанском яз.: "v149^4" экз.",
    fi, 
    if p(v151) then'\par \b   По разделам знаний:\b0 ', 
       if p(v151^1) then'\par по разделу 1 (',&unifor('Krzn.mnu|1'),'): ',v151^1 fi, 
       if p(v151^2) then'\par по разделу 2 (',&unifor('Krzn.mnu|2'),'): ',v151^2 fi, 
       if p(v151^3) then'\par по разделу 3 (',&unifor('Krzn.mnu|3'),'): ',v151^3 fi, 
       if p(v151^4) then'\par по разделу 4 (',&unifor('Krzn.mnu|4'),'): ',v151^4 fi, 
       if p(v151^5) then'\par по разделу 5 (',&unifor('Krzn.mnu|5'),'): ',v151^5 fi, 
       if p(v151^6) then'\par по разделу 6 (',&unifor('Krzn.mnu|6'),'): ',v151^6 fi, 
       if p(v151^7) then'\par по разделу 7 (',&unifor('Krzn.mnu|7'),'): ',v151^7 fi, 
       if p(v151^8) then'\par по разделу 8 (',&unifor('Krzn.mnu|8'),'): ',v151^8 fi, 
       if p(v151^9) then'\par по разделу 9 (',&unifor('Krzn.mnu|9'),'): ',v151^9 fi, 
       if p(v151^0) then'\par по разделу 10 (',&unifor('Krzn.mnu|10'),'): ',v151^0 fi, 
       if p(v151^a) then'\par по разделу 11 (',&unifor('Krzn.mnu|11'),'): ',v151^a fi, 
       if p(v151^b) then'\par по разделу 12 (',&unifor('Krzn.mnu|12'),'): ',v151^b fi, 
       if p(v151^c) then'\par по разделу 13 (',&unifor('Krzn.mnu|13'),'): ',v151^c fi, 
       if p(v151^d) then'\par по разделу 14 (',&unifor('Krzn.mnu|14'),'): ',v151^d fi, 
       if p(v151^e) then'\par по разделу 15 (',&unifor('Krzn.mnu|15'),'): ',v151^e fi, 
       "\par другое: "v151^X, 
    else 
       if p(v47) then'\par \b   По разделам знаний:\b0 ', 
          if p(v47^1) then'\par по разделу 1 (',&unifor('Krzn.mnu|1'),'): ',v47^1 fi, 
          if p(v47^2) then'\par по разделу 2 (',&unifor('Krzn.mnu|2'),'): ',v47^2 fi, 
          if p(v47^3) then'\par по разделу 3 (',&unifor('Krzn.mnu|3'),'): ',v47^3 fi, 
          if p(v47^4) then'\par по разделу 4 (',&unifor('Krzn.mnu|4'),'): ',v47^4 fi, 
          if p(v47^5) then'\par по разделу 5 (',&unifor('Krzn.mnu|5'),'): ',v47^5 fi, 
          if p(v47^6) then'\par по разделу 6 (',&unifor('Krzn.mnu|6'),'): ',v47^6 fi, 
          if p(v47^7) then'\par по разделу 7 (',&unifor('Krzn.mnu|7'),'): ',v47^7 fi, 
          if p(v47^8) then'\par по разделу 8 (',&unifor('Krzn.mnu|8'),'): ',v47^8 fi, 
          if p(v47^9) then'\par по разделу 9 (',&unifor('Krzn.mnu|9'),'): ',v47^9 fi, 
          if p(v47^a) then'\par по разделу 10 (',&unifor('Krzn.mnu|10'),'): ',v47^a fi, 
          if p(v47^b) then'\par по разделу 11 (',&unifor('Krzn.mnu|11'),'): ',v47^b fi, 
          if p(v47^c) then'\par по разделу 12 (',&unifor('Krzn.mnu|12'),'): ',v47^c fi, 
          if p(v47^d) then'\par по разделу 13 (',&unifor('Krzn.mnu|13'),'): ',v47^d fi, 
          if p(v47^e) then'\par по разделу 14 (',&unifor('Krzn.mnu|14'),'): ',v47^e fi, 
          if p(v47^f) then'\par по разделу 15 (',&unifor('Krzn.mnu|15'),'): ',v47^f fi, 
          "\par другое: "v47^X, 
fi fi fi'\par  }'
