0
// Работает в Мастере поступления
// запись SZ - корректируется / добавляется повторение, в котором считает кол-во полученных экземпляров по записям БО полям 62
IF
if v920: 'SZ' and p(v62) then '1' fi
/*---- отбираю все книги по организации и заказу - беру поля 62
//--- в поле 111 mfn книг по организации-заказу записи SZ
DEL
111
*


ADD
111

&uf('7,?BORZ=',,&uf('Av62^K#1'),,'!',,&uf('Av62^6#1'),'?,mfn/' )

//--- в поле 112 все поля 62 из записей книг по организации-заказу записи SZ
DEL
112
*


ADD
112

&uf('7,?BORZ=',,&uf('Av62^K#1'),,'!',,&uf('Av62^6#1'),'?,(v62/)' )

//---- в поле g13 только КСУ из книг по заказу
ADD
113

&uf('+7W13#',,,,&uf('7,?BORZ=',,&uf('Av62^K#1'),,'!',,&uf('Av62^6#1'),'?,(v62^U/)' )  ),,,,&uf('+7G13'),,,(g13/)

DEL
114
*


/*----цикл по номерам КСУ - v113 - к каждому номеру сумму поступивших
//----в поле 114 сумма экземпляров по конкретному КСУ
REPEAT
ADD
114

'^U',,&uf('Av113#1'),,,'^G',,f(rsum( (if p(v112) then if p(v112^U) then if v112^U=&uf('Av113#1') then if  v112^K=&uf('Av62^K#1') then if v112^B=&uf('Av62^6#1') then v112^G,',',,fi fi fi fi fi) ),0,0),,'^I',&uf('3')

DEL
113
1


UNTIL
if p(v113) then '1' fi
DEL
113
*


//----цикл по v114 - дописываю поля 114 кол-вом наименований
REPEAT
REP
114
1
&uf('Av114#1'),,,'^J',,f(rsum((if p(v111) then ,if ref(val(v111),v62^U|,|): &uf('Av114^U#1') then '1,' fi fi)),0,0) 

ADD
113

&uf('Av114#1')

DEL
114
1


UNTIL
if p(v114) then '1' fi
ADD
114

(v113/)

//----добавление из поля 114 в поле 62,если еще нет такого КСУ
REPEAT
ADD
62

if rsum( (if p(v62) then if v62^U=&uf('Av114^U#1') then '1,' break fi fi) )=0 then &uf('Av114#1') fi

//----замена поля 62 из поля 114 если КСУ есть
REP
62
F
(if p(v62) then v62,,,if v62^G=''  then if v62^U<>'' and v62^U=&uf('Av114^U#1') then if a(v62^G) then '^G' fi,,&uf('Av114^G#1') fi,,,fi,,,if a(v62^J)  then if v62^U<>'' and v62^U=&uf('Av114^U#1') then if a(v62^J) then '^J' fi,,&uf('Av114^J#1') fi,,,fi,,,if a(v62^I) then '^I',&uf('Av114^I#1') fi,fi/)

//----исправляю ^G ^J по данным из БО
REP
62^G
F
(if p(v62) then if p(v62^G) then if v62^U<>'' and &uf('Av114^G#1')<>'' and v62^U=&uf('Av114^U#1') then &uf('Av114^G#1') else v62^G fi  else # fi fi/)

REP
62^J
F
(if p(v62) then if p(v62^J) then if v62^U<>'' and &uf('Av114^J#1')<>'' and v62^U=&uf('Av114^U#1') then &uf('Av114^J#1') else v62^J fi  else # fi fi/)

DEL
114
1


UNTIL
if p(v114) then '1' fi
FI
DEL
111
*


DEL
112
*


DEL
113
*


DEL
114
*


