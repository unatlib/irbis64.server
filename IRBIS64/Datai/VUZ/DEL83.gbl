1
vDel83.wss
Параметры удаления
//12 PUTLOG
// Используется в УПЛ удаление семестра из 83^f
//16 использую для удаления дисциплины целиком
// удаление поля 83 по совпадения с подполями поля 991
// на записи DISC
// 993^? - признак переноса в поле 942
// 993^@ - список дополнительных БД каталога
DEL
993
*


ADD
993

&uf('+7W993#','%1')

// убираю данные из опросного листа
DEL
991
F
(if p(v991) then if v991^?<>'' or v991^@<>'' then '1' else '0' fi fi/)

IF
if v920: 'DISC' then '1' fi
IF
if v991^X='1' then '1' fi
//-----------------------------удаление дисциплин целиком
//----имя БД в G100
DEL
993
*


ADD
993

g993,,,&uf('+7W100#',,,if &unifor('IPRIVATE,DBN,')<>'' then &unifor('IPRIVATE,DBN,') else 'IBIS' fi)

REP
993^@
1
if v993^@<>'' then v993^@,,,if v993^@: '.' then  else '.mnu' fi fi,,,,,,,

//.................. в G100 имена всех БД
IF
if v993^@<>'' then '1' fi
ADD
991

(if  &uf('+5T',,&uf('Av993^@#1'),,)<>'' then &uf('+7U100#',&uf('+5T',,&uf('Av993^@#1'),,) ) else break fi/),,&uf('+7G100'),,,&uf('+7G100')

FI
REPEAT
//------------------------------------------------------цикл по отмеченным дисциплинам
IF
if &uf('+97',v3^0)=&uf('+97',&uf('Av991^I#1')) then '1' fi
// ДА дисциплина по идентификатору
//.............................................цикл по БД каталога
DEL
994
*


ADD
994

(g100/),,,&uf('+7W10#')

REPEAT
//----------------удаление из каталога
CORREC
&uf('Av994#1')
mpu,v3^0,&uf('+7W1#')
"IDD="v3^0
XXXXXXXXXXXXXXXXXXX
DEL
691
F
(if p(v691) then if p(v691^I) then if &unifor('Av1001#1')<>''and &uf('+97',v691^I)=&unifor('Av1001#1') then '1',&uf('+7W1#1') else  '0' fi else '0' fi fi/)
XXXXXXXXXXXXXXXXXXX
PUTLOG
if val(g1)>0 then 'Удалена в каталоге ',v1001,'. MFN=',f(val(mfn),0,0) fi
DEL
1001
*
XXXXXXXXXXXXXXXXXXX
XXXXXXXXXXXXXXXXXXX
END
XXXXXXXXXXXXXXXXXXX
XXXXXXXXXXXXXXXXXXX
XXXXXXXXXXXXXXXXXXX
XXXXXXXXXXXXXXXXXXX
DEL
994
1


UNTIL
if p(v994) then '1' fi
//....................................конец удаления в каталогах
//---удаление у студентов
IF
if val(&unifor('IMAIN,ACCESSRDR,1'))=1 then '1' else '0' fi
CORREC
'RDR'
mpu,v3^0,&uf('+7W1#')
"IDD="v3^0
XXXXXXXXXXXXXXXXXXX
DEL
69
F
(if p(v69) then if p(v69^D) then if &uf('Av1001#1')=&uf('+97',v69^D) then '1',&uf('+7W1#1') else '0' fi else '0' fi fi/)

PUTLOG
if val(g1)>0 then 'Удалена в записи студента ',v30,'. MFN=',f(val(mfn),0,0) fi
DEL
1001
*


END
//---конец ACCESSRDR
FI
//---удаление в контингентах
CORREC
'*'
v3^0,&uf('+7W1#')
"IDV="v3^0
XXXXXXXXXXXXXXXXXXX
DEL
69
F
(if p(v69) then if p(v69^D) then if &uf('Av1001#1')=&uf('+97',v69^D) then '1',&uf('+7W1#1') else '0' fi else '0' fi fi/)

PUTLOG
mdl,,if val(g1)>0 then 'Удалена в контингенте ',v68,'. MFN=',f(val(mfn),0,0) fi,mpl
END
//---удаление дисциплины
PUTLOG
'Удалена дисципина ',,v3^A
DELR
XXXXXXXXXXXXXXXXXXX
XXXXXXXXXXXXXXXXXXX
XXXXXXXXXXXXXXXXXXX
XXXXXXXXXXXXXXXXXXX
//----конец очередной дисциплины по идентификатору
FI
DEL
991
1


UNTIL
if p(v991) then '1' fi
//--------------------конец удаления дисциплин целиком
FI
IF
if v991^X='1' then else '1' fi
DEL
1083
*


ADD
1083

(v83/)

REP
991
1
mpu,v991

CHA
991^F
1
'10'
'A'
CHA
991^F
1
'11'
'B'
CHA
991^F
1
'12'
'C'
CHA
83^F
*
'10'
'A'
CHA
83^F
*
'11'
'B'
CHA
83^F
*
'12'
'C'
CHA
83^F
F
(if p(v83) then if ( &unifor('Av991^A#1')<>''and &uf('+97',v83^A)=&unifor('Av991^A#1') or &unifor('Av991^A#1')='') and (&unifor('Av991^n#1')<>''and &uf('+97',v83^n)=&unifor('Av991^n#1') or &unifor('Av991^n#1')='') and (&unifor('Av991^c#1')<>''and &uf('+97',v83^c)=&unifor('Av991^c#1') or &unifor('Av991^c#1')='') and (&unifor('Av991^v#1')<>''and &uf('+97',v83^v)=&unifor('Av991^v#1') or &unifor('Av991^v#1')='') and (&unifor('Av991^o#1')<>''and &uf('+97',v83^o)=&unifor('Av991^o#1') or &unifor('Av991^o#1')='') and (&unifor('Av991^f#1')<>''and v83^f:&unifor('Av991^f#1')) then &unifor('Av991^f#1') else # fi else # fi/)
''
CHA
83^F
*
'A'
'10'
CHA
83^F
*
'B'
'11'
CHA
83^F
*
'C'
'12'
CHA
83^F
*
'//'
'/'
CHA
83^F
*
'^F/'
'^F'
//----- Удаление поля 83
DEL
83
F
(if p(v83) then if v83^f='' or v83^f='/' then'1'else'0' fi fi/)
XXXXXXXXXXXXXXXXXXX
IF
if v83<>v1083 then '1' fi
PUTLOG
'Откорректирована дисциплина ',v3^A,'. MFN=',f(val(mfn),0,0)
FI
DEL
1083
*


FI
//----конец записи DISC
FI
DEL
991
*



