0
// записи FAK создаются из записей VUZ
// отбор - все записи VUZ
// есть ли уже запись ФАК-ФО - термин в v111
IF
if p(v68) then '1' fi
DEL
111
*


ADD
111

'FFO=',v68^A,"ВСЕ"n68^A,v68^L,v68^T'-',v68^O,"ВСЕ"n68^O,

DEL
112
*


ADD
112

(if p(v69^B) then &uf('Pv69^B')/ fi/)

IF
if val(&unifor('JVUZ,',v111))>0 then '1' fi
// запись FAK уже есть
CORREC
'*'
(v112/)
mpu,v111

// добавляю кафедру с проверкой на дубль 
REPEAT
IF
if rsum((if p(v5) then if &uf('+97',v5)=&uf('+97',&uf('Av1001#1')) then '1,' else '0,' fi fi))<=0 then '1' fi
ADD
5

&uf('Av1001#1')

FI
DEL
1001
1


UNTIL
if p(v1001) then '1' fi
END
FI
IF
if val(&unifor('JVUZ,',v111))<=0 then '1' fi
// записи FAK-FO еще нет - создаю
NEWMFN
'*'
ADD
920

'FAK'

ADD
93

if v68^A<>'' then v68^A else 'ВСЕ' fi

ADD
91

v68^L

ADD
92

v68^T

ADD
98

if v68^O<>'' then v68^O else 'ВСЕ' fi

// на случай одинаковой кафедры
ADD
5

(if p(v69^B) then &uf('Pv69^B')/ fi/)

PUTLOG
'Создана запись факультета ',,v93,," - "v98
END
FI
DEL
111
*


DEL
112
*


FI
