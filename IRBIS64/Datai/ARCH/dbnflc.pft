IF v920='SZPRF' then else 
   if (l("I="v903)<=0) or (val(ref(l("I="v903),mfn))=val(mfn)) then /'0' else 
      if v920:'asp'then/'0' else/ 
      '1 Ошибка : Дублетный шифр ' v903,ref(l("I="v903),' (см. N ',f(val(mfn),0,0),')') 
   fi fi/ 
   if p(v19) then 
      if v19:'^b' then 
         if L("B="d19,v19^a*2" ",v19^b) <= 0 or val(ref(l("B="d19,v19^a*2" ",v19^b),mfn))=val(mfn) 
         then/'0' else/'1Дублетный Идентификационный Номер: ',v19^a*2" ",v19^b,
                       ref(l("B="d19,v19^a*2" ",v19^b),' (см. N ',f(val(mfn),0,0),')') 
   fi fi fi,
   if p(v200) then /'0' else /'1 Ошибка : Отсутствует заглавие' fi 
   IF p(v910) then 
      IF v905^3='1' or &uf('IPRIVATE,3,')='1' then else 
         IF s(v905^3,&uf('IPRIVATE,3,')):'22'or s(v905^3,&uf('IPRIVATE,3,')):'32' then else 
            (if p(v910) then 
                if p(v910^b)then 
                   if &unifor('Av920#1')='NJ'or &unifor('Av920#1')='NJP'or v910^a='R'or &unifor('Av215^y#1')<>'' 
                   then /'0' else 
                      if l(|IN=|v910^b)<=0 or val(ref(l(|IN=|v910^b),mfn))=val(mfn)then/'0' 
                      else/'2Дублетный инв.номер: 'v910^b,ref(l(|IN=|v910^b),' (см.N ',mfn,')') 
             fi fi fi fi/)
         FI,
         IF s(v905^3,&uf('IPRIVATE,3,')):'21' or s(v905^3,&uf('IPRIVATE,3,')):'31' then else 
            (if p(v910) then 
                if p(v910^h)then 
                   if l(|IN=|v910^h)<=0 or val(ref(l(|IN=|v910^h),mfn))=val(mfn)then/'0' 
                   else/'2Дублетный Штрих-код: 'v910^h,ref(l(|IN=|v910^h),' (см.N ',mfn,')') 
                   fi 
                else/'0' 
             fi fi)   
         FI,  
         IF s(v905^3,&uf('IPRIVATE,3,')):'3' or s(v905^3,&uf('IPRIVATE,3,'))='' then 
            &unifor('+7W11#'),&unifor('+7W10#'),&unifor('+7W21#'),&unifor('+7W20#'), 
            &unifor('+7W31#'),&unifor('+7W30#'),&unifor('+7W41#'),&unifor('+7W40#'), 
            IF s(v905^3,&uf('IPRIVATE,3,')):'32' then else 
               &unifor('+7W11#'(if 'U C 6':v910^a then else if v910^a:'R' then ' ' &uf('G2/'v910^b) else | |v910^b fi fi/)), 
               &uf('+7W10#'),&uf('+7W10#'f(rsum((if p(g11) then g10,';1;' fi)),0,0)),
               &uf('+7W21#'(g11/)),&uf('+7G21'),
               &uf('+7W20#'),&uf('+7W20#'f(rsum((if p(g21) then g20,';1;' fi)),0,0)),
               if val(g10)=val(g20) then else /'2 В документе есть дублетные инв. номера: ' fi, 
               IF s(v905^3,&uf('IPRIVATE,3,'))='' then &uf('+7S11#21'),(g11/), 
            FI FI,
            IF s(v905^3,&uf('IPRIVATE,3,')):'31' then else 
               &unifor('+7W31#'(| |v910^h/)), 
               &uf('+7W30#'),&uf('+7W30#'f(rsum((if p(g31) then g30,';1;' fi)),0,0)),
               &uf('+7W41#'(g31/)),&uf('+7G41'),
               &uf('+7W40#'),&uf('+7W40#'f(rsum((if p(g41) then g40,';1;' fi)),0,0)),
               if val(g40)=val(g30) then else /'2 В документе есть дублетные штрих-коды: ' fi, 
               IF s(v905^3,&uf('IPRIVATE,3,'))='' then &uf('+7S31#41'),(g31/), 
      FI FI FI FI,
      (if p(v910) then 
          if p(v910^a) then/'0' else/'2 Ошибка : отсутствует статус экз-ра   'v910^b,|   |v910^c fi/ 
          if '2 R C 7':v910^a then else 
             if s(v910^b,v910^h)<>''then/'0' 
             else/'2 Ошибка : Отсутствует инв.номер и штрих-код экз-ра   ' v910^c 
       fi fi fi/) 
   else 
      if p(v951) then else/'2 Ошибка : отсутствуют сведения об экземплярах' 
   fi fi/ 
FI 
