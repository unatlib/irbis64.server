'</>',
if v920:'MUSP' THEN '<b><dd>Описание музейного предмета </b><br><br>',
if v951^a<>'' then &uf('+7W951#'),(if p(v951^a) then &uf('+7U951#'v951^a/) fi),
'<table width="100%">',
'<tr>',
'<td   valign="top">',
'<A HREF="IRBIS:3?PATH=10&FILENAME=',&uf('Ag951#1'),'">',
'<IMG  style="width:150 px" SRC="IRBIS:10,,',&uf('Ag951#1'),'"></A>'
'</td>',
'<td>',
fi,mhl,
'<br><b>Название: </b> ',v200^v,if p(v200^v)then" : "v200^a else v200^a fi, 
"<br><b>Параллельные названия: </b> "| = |+v510^d,|, |v924, 
If s(v700,v961)<>'' then '<br><b>Автор </b>: ',v961,v700 fi,
If p(v702) then '<br><b>Лица, имеющие отношение</b>: 'v702^a,' ',v702^b,'(', '<b>',&unifor('F1'v702^4),'</b>',')' fi  
IF s(v962,v710)<>'' then'<br><b>Коллектив: </b>'v962^s".",v962^a, 
   if s(v962^n,v962^c)<>''then' ('v962^n" ; ",v962^c,')'fi,". "v962^b, 
   if s(v962^d,v962^f,v962^e)<>''then' ('v962^d" ; ",v962^f, 
      if p(v962^f)then" ; "v962^e else v962^e fi," / "v962^h," / "v962^i')' 
   fi,". "d962,
   if a(v962) then v710^s".",v710^a, 
      if s(v710^n,v710^c)<>''then' ('v710^n" ; ",v710^c,')'fi,". "v710^b, 
      if s(v710^d,v710^f,v710^e)<>''then' ('v710^d" ; ",v710^f, 
         if p(v710^f)then" ; "v710^e else v710^e fi," / "v710^h," / "v710^i')' 
      fi,". "d710,
fi fi,
"<br><b>Общее обозначение: </b> "v461, 
'<br><b>Тип предмета: </b> ',if p(v116) then &unifor('K116a0.mnu|'v116^b) ', ' fi,d900^x,  
 &unifor('Kznm.mnu|'v900^x), 
'<br><b>Идентификационный номер: </b> ',if v19^a:'z' then else v19^a.3| | fi,v19^b, 
 if p(v210) then '<br><b>Датировка: </b> 'v210^d,fi,' ',v210^5,   
 if p(v215) then '<br><b>Количественные характеристики: </b> 'v215^a,", "v215^4, 
 if p(v215^z) then ", "d215^a fi,"состоит из "v215^z" единиц хранения" , fi,
 if p(v488) then ,Opris488_H, fi,
 if p(v10^d) then '<br><b>Стоимость закупочная: </b> ',v10^d fi,
 if p(v10^d) then ' ',v10^c,if a(v10^c) then &unifor('Korg.mnu|3') fi fi, 
 if p(v10^1) then '<br><b>Стоимость страховая: </b> ',v10^1 fi,
 if p(v10^1) then ' ',v10^c,if a(v10^c) then &unifor('Korg.mnu|3') fi fi, 
 if p(v391) then '<br><b>Примечания о наличии автографов: </b> ',|, |+v391 fi, 
 If p(v300) then '<br><b>Общие примечания: </b> ',|, |+v300 fi, 
 if p(v397) then '<br><b>Примечания о технике изготовления: </b> ',|, |+v397 fi,
  ,oant_H, mhl, 
 if p(v398) then '<br><b>Клейма, штампы: </b> ',|, |+v398 fi,     
 if p(v395) then '<br><b>Надписи, даты: </b> ',|, |+v395 fi,     
 if p(v912) then '<br><b>Примечания о языке: </b> ',|, |+v912 fi,     
 if p(v399) then '<br><b>Состояние предмета: </b> ',|, |+v399 fi,     
 if p(v430) then '<br><b>Переатрибуция: </b> ',|, |+v430 fi,     
 if p(v470) then '<br><b>Публикация: </b> ',|, |+v470 fi,
(if p(v951^I) then '<br><A HREF="',v951^I,'">',if p(v951^T) then v951^T else v951^I fi,'</A>', fi), 

 if p(v607)then'<br><b>Этническая классификация: </b> ' 
    (if p(v607)then |<br>  |+v607^a,| -- |v607^b,| -- |v607^c,| -- |v607^d,| -- |v607^g, 
    | -- |v607^e,| -- |v607^o,if v607=v607^h then v607^h else |, |v607^h fi fi) fi, 
 if p(v606) then '<br><b>Тематическая классификация: </b> '(| ; |+d606^a,v606^a,
    if &unifor(|Kktt.mnu\|v606^a)<>'' then ' (',&unifor(|Kktt.mnu\|v606^a),')'fi) fi, 
 "<br><b>Дескрипторы: </b> "v965+| -- |, 
 if &unifor('Av905^f#1')<>''or &unifor('IPRIVATE,FPK,')<>''then else 
 "<br><b>Ключевые слова: </b> "d610,v610+|--|, 
 fi, 
 if p(v902) then '<br><b>Держатель предмета: </b> 'v902^a fi, 
 '<br><b>Дата поступления: </b> ',v910^c, 
 if p(v910^6) then '<br><b>Способ поступления: </b> ',&unifor('Kpst.mnu|'v910^6) fi, 
 if p(v910^t) then '<br><b>История поступления: </b> ',v910^t fi, 
 if p(v910^j) then '<br><b>Характер поступления: </b> ',&unifor('Kcharp.mnu|'v910^J) fi, 
 if p(v910^f) then '<br><b>Канал поступления: </b> ',&unifor('Kkpm.mnu|'v910^f) fi, 
 if p(v910^d) then '<br><b>Место хранения: </b> ',&unifor('Kmhrm.mnu|'v910^d)fi, 
if v951^a<>'' then 
'</td>',
'</tr>',
'</table>', 
fi,
mpl,fi 
