1
CreateVuz.wss
Определите способ корректировки
// формирование в БД VUZ записей дисциплин по полям 691
// 991 - опрос
// ^A - связь с БД VUZ по имени дисциплины
// ^B - связь с БД VUZ по идентификатору дисциплины
// ^C - связь с БД VUZ по двум параметрам (имя + идентификатор)
//12 PUTLOG
DEL
3691
*


IF
if v991^A='' and v991^B='' and v991^C='' then '1' fi
PUTLOG
'Не задан способ идентификации дисциплины в БД VUZ'
FI
IF
if v991^A<>'' or v991^B<>'' or v991^C<>'' then '1' fi
IF
if v991^A<>'' and v991^B<>'' then '1' fi
REP
991
1
v991,if v991^C='' then '^C1' fi

FI
DEL
1691
*


ADD
1691

&uf('+7W5#'),(if p(v691) then v691 fi/)

//========================================== цикл по полю 691
// сначала в глобальную G5 записываю идентификатоы/имена дисциплин чтобы взять уникальные 
REPEAT
DEL
2691
*


ADD
2691

&uf('Av1691#1')

IF
if v991^C='' then '1' fi
// задан один параметр
IF
if v991^A<>'' and v991^B='' or v991^A='' and v991^B<>'' then '1' fi
//------------------------- идентификация по имени или идентификатору
// есть ли уже запись дисциплины ?
IF
if v991^A<>'' and val(&uf('JVUZ,DISCN=',v2691^D))=0 or v991^B<>'' and val(&uf('JVUZ,IDD=',v2691^I))=0 then '1' fi
// нет записи с именем дисциплины - на создание новой
ADD
1001

&uf('+7U5#',"^A"v2691^D,"^0"v2691^I,"^S"v2691^S,,,"^K"v2691^K,,,,"^B"v2691^B,)

FI
IF
if v991^A<>'' and val(&uf('JVUZ,DISCN=',v2691^D))>0 or v991^B<>'' and val(&uf('JVUZ,IDD=',v2691^I))>0 then '1' fi
// уже есть
ADD
3691

mpu,if v991^A<>'' then &uf('DVUZ,!DISCN=',v2691^D,'!,v3') else if v991^B<>'' then &uf('DVUZ,!IDD=',v2691^I,'!,v3') fi fi

FI
// конец идентификация по имени или идентификатору
FI
// конец задан один параметр
FI
IF
if v991^C<>'' then '1' fi
//----------------------идентификация по двум параметрам
// есть ли уже запись дисциплины ?
IF
if val(&uf('JVUZ,DISCN=',v2691^D))=0 and val(&uf('JVUZ,IDD=',v2691^I))=0 then '1' fi
// нет записи с именем дисциплины - создаю новую
ADD
1001

&uf('+7U5#',"^A"v2691^D,"^0"v2691^I,"^S"v2691^S,,,"^K"v2691^K,,,,"^B"v2691^B,)

FI
IF
if val(&uf('JVUZ,DISCN=',v2691^D))>0 or val(&uf('JVUZ,IDD=',v2691^I))>0 then '1' fi
// уже есть
ADD
3691

mpu,if val(&uf('JVUZ,DISCN=',v2691^D))>0 then &uf('DVUZ,!DISCN=',v2691^D,'!,v3') else if val(&uf('JVUZ,IDD=',v2691^I))>0 then &uf('DVUZ,!IDD=',v2691^I,'!,v3') fi fi

FI
// конец оба параметра
FI
DEL
1691
1


UNTIL
if p(v1691) then '1' fi
//=======================конец первого цикла по 691
DEL
2691
*


// есть ли данные для создания дисциплины?
IF
if G5<>'' then '1' fi
DEL
1691
*


ADD
1691

&uf('+7G5'),(G5/)

//=============================цикл создания записей по глобальной G5
REPEAT
DEL
2691
*


ADD
2691

&uf('Av1691#1')

// проверка - есть ли идентификатор
IF
if v2691^0='' then '1' fi
//-------------- нет идентификатора - создаю

DEL
111
*


IF
if val(&uf('JVUZ,IDD=',v2691^A.5))>0 or s((|!|v113|!|)): v2691^A.5 then '1' fi
ADD
111

'0'

REPEAT
IF
if val(&uf('JVUZ,IDD=',v2691^A.5,f(val(v111),0,0)))>0 or s((|!|v113|!|)):  s( v2691^A.5,,f(val(v111),0,0)  ) then '1' fi
REP
111
1
f(val(v111)+1,0,0)

FI
// запоминаю идентификатор, котрый ,будет присвоен
UNTIL
if val(&uf('JVUZ,IDD=',v2691^A.5,f(val(v111),0,0)))>0 or s((|!|v113|!|)):  s( v2691^A.5,,f(val(v111),0,0)  ) then '1' else '0' fi
FI
ADD
113

v2691^A.5,v111

//идентификатор создан в 2691^0
REP
2691
1
v2691,'^0',v2691^A.5,v111

//----------
FI
DEL
111
*


NEWMFN
'VUZ'
ADD
920

'DISC'

ADD
3

"^A"v2691^A,"^0"v2691^0

ADD
4

v2691^S

ADD
6

v2691^K

ADD
5

v2691^B

DEL
1001
*


END
PUTLOG
'Создана новая запись дисциплины - ',v2691^A
//запоминаю из какого повторения 691 создана запись
ADD
3691

mpu,v2691


DEL
1691
1


UNTIL
if p(v1691) then '1' fi
//==============================конец цикла создания VUZ по переменной G5
// конец G5 не пустое
FI
//обновление поля 691, которое создало запись VUZ - по 3691
IF
if p(v3691) then '1' fi
// -------цикл по 3691 - солздаю или корректирую подполе I
REPEAT



REP
691
F
(if p(v691) then v691,,,if &uf('Av991^A#1')<>'' and &uf('Av3691^A#1')=&uf('+97',v691^D) or &uf('Av991^B#1')<>'' and &uf('Av3691^0#1')=&uf('+97',v691^I) or &uf('Av991^C#1')<>'' and &uf('Av3691^A#1')=&uf('+97',v691^D) and &uf('Av3691^0#1')=&uf('+97',v691^I) then '^X',&uf('Av3691^0#1') fi fi/)

REP
691
F
(if p(v691) then v691,if a(v691^I) then |^I|v691^X fi fi/)

REP
691^I
F
(if p(v691) then if p(v691^I) then  if v691^X<>'' then v691^X else v691^I fi else # fi fi/)

DEL
691^X
*


DEL
3691
1


UNTIL
if p(v3691) then '1' fi
//-------конец цикла по 3691
// конец есть поле 3691
FI
// конец задан параметр
FI
DEL
991
*


DEL
2691
*


DEL
113
*



