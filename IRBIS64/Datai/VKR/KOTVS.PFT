&unifor('+1W#'''), 
"KO ВС в 692 для 991^a (выделенная категория - ВК"d10000, 
&unifor('+1W1#'''), 
&unifor('+1W1#',(if p(v692) then 
                    if (v692^c:'v'AND (&unifor('Av991^u#1')<>''and p(v692^b) and v692^b=&unifor('Av991^u#1') or 
                                       &unifor('Av991^u#1') =''and v692^b:s('/',&unifor('30'))) 
                                  AND (&unifor('Av991^a#1')<>''and p(v692^a) and v692^a=&unifor('Av991^a#1') or 
                                       s(v692^a,&unifor('Av991^a#1'))='')) 
                    then f(val(v692^f,if a(v692^f) then v692^l fi),0,2) fi fi)), 
"Экземпляры для 991^a(ВК)"d10000, 
&unifor('+1W2#'''), 
&unifor('+1W2#',f(rsum((if p(v910^c) then 
                           if &unifor('Av991^a#1')<>'' and p(v910^t) and v910^t:&unifor('Av991^a#1')  then 
                              if v910^a:'U' then f(val(v910^1,|1|n910^1)*val(v910^=,|1|n910^=),0,0)';' else 
                                 if 'C 6 2 7':v910^a then else f(val(v910^=,|1|n910^=),0,0)';' 
                        fi fi fi fi)),0,0)), 
"Студ. ВС для 991^a"d10000, 
&unifor('+7W3#'),&uf('+7W333#'),
if p(v991^a) then 
   if &unifor('IPRIVATE,DATFINOS,')<>'' and val(&unifor('3')) > val(&unifor('IPRIVATE,DATFINOS,')) or 
      &unifor('IPRIVATE,DATFINOS,')='' and val(&unifor('34')) < 9 then 
      if val(&unifor('IMAIN,ACCESSRDR,1'))=1 then 
         &unifor('+7W333#',(if p(v691) then 
                               if p(v691^9) then 
                                  if &unifor('Av991^a#1')<>'' and v691^9:&unifor('Av991^a#1') 
                                  then 
                                     &unifor('7RDR,!UPS='v691^9,'!,mfn/')/ 
                                     &unifor('7RDR,!?',v691^9,'!,mfn/')/ 
                                     &unifor('7RDR,!',&unifor('G2*'v691^9),'!,mfn/')/ 
                            fi fi fi/)),&uf('+7G333'),
         &uf('+7W3#'f(rsum((if p(g333) then '1;'fi/)),0,0))                      
      else 
         &uf('+7W3#'f(rsum((if p(v691) then 
                               if p(v691^9) then 
                                  if &unifor('Av991^a#1')<>'' and v691^9:&unifor('Av991^a#1') 
                                  then 
                                  &unifor('DVUZ,"!',v691^9,'",v68^Z')';' 
                            fi fi fi)),0,0)), 
fi fi fi,
"Экземпляры доп. для 991^a "d10000, 
&unifor('+1W4#'''), 
if v910^t<>'' then 
   if rsum((if p(v910) then if &unifor('Pv910^T')<>'' then '1;'fi fi))> 1 then  
&unifor('+1W4#',f(rsum((if p(v910^c) then 
                           if a(v910^t) then 
                              if v910^a:'U' then f(val(v910^1,|1|n910^1)*val(v910^=,|1|n910^=),0,0)';' else 
                                 if 'C 6 2 7':v910^a then else f(val(v910^=,|1|n910^=),0,0)';' 
                        fi fi fi fi)),0,0)), 
fi fi, 
"Студ. BС не ВК"d10000, 
&unifor('+7W5#'),&uf('+7W555#'),
if a(v991^a) then 
   if &unifor('IPRIVATE,DATFINOS,')<>'' and val(&unifor('3')) > val(&unifor('IPRIVATE,DATFINOS,')) or 
      &unifor('IPRIVATE,DATFINOS,')='' and val(&unifor('34')) < 9 then 
      if v910^t<>'' then 
         if rsum((if p(v910) then if &unifor('Pv910^T')<>'' then '1;'fi fi))> 1 then 
            if val(&unifor('IMAIN,ACCESSRDR,1'))=1 then 
               &unifor('+7W555#',(if p(v691) then 
                                     if p(v691^9) then 
                                        if &unifor('Knazn.mnu|1')<>'' and v691^9:&unifor('Knazn.mnu|1') or 
                                           &unifor('Knazn.mnu|2')<>'' and v691^9:&unifor('Knazn.mnu|2') or 
                                           &unifor('Knazn.mnu|3')<>'' and v691^9:&unifor('Knazn.mnu|3') or 
                                           &unifor('Knazn.mnu|4')<>'' and v691^9:&unifor('Knazn.mnu|4') or 
                                           &unifor('Knazn.mnu|5')<>'' and v691^9:&unifor('Knazn.mnu|5') 
                                        then else 
                                           &unifor('7RDR,!UPS='v691^9,'!,mfn/')/ 
                                           &unifor('7RDR,!?',v691^9,'!,mfn/')/ 
                                           &unifor('7RDR,!',&unifor('G2*'v691^9),'!,mfn/')/ 
                                  fi fi fi/)),&uf('+7G555'),
               &uf('+7W5#'f(rsum((if p(g555) then '1;'fi/)),0,0))                      
            else 
               &uf('+7W5#'f(rsum((if p(v691) then 
                                     if p(v691^9) then 
                                        if &unifor('Knazn.mnu|1')<>'' and v691^9:&unifor('Knazn.mnu|1') or 
                                           &unifor('Knazn.mnu|2')<>'' and v691^9:&unifor('Knazn.mnu|2') or 
                                           &unifor('Knazn.mnu|3')<>'' and v691^9:&unifor('Knazn.mnu|3') or 
                                           &unifor('Knazn.mnu|4')<>'' and v691^9:&unifor('Knazn.mnu|4') or 
                                           &unifor('Knazn.mnu|5')<>'' and v691^9:&unifor('Knazn.mnu|5') 
                                        then else 
                                           &unifor('DVUZ,"!',v691^9,'",v68^Z')';' 
                                 fi fi fi)),0,0)), 
         fi fi 
      else  
         if val(&unifor('IMAIN,ACCESSRDR,1'))=1 then 
            &unifor('+7W555#',(if p(v691) then 
                                  if p(v691^9) then 
                                     &unifor('7RDR,!UPS='v691^9,'!,mfn/')/ 
                                     &unifor('7RDR,!?',v691^9,'!,mfn/')/ 
                                     &unifor('7RDR,!',&unifor('G2*'v691^9),'!,mfn/')/ 
                               fi fi/)),&uf('+7G555'),
            &uf('+7W5#'f(rsum((if p(g555) then '1;'fi/)),0,0))                      
         else 
            &uf('+7W5#'f(rsum((if p(v691) then 
                                  if p(v691^9) then 
                                     &unifor('DVUZ,"!',v691^9,'",v68^Z')';' 
                                  fi fi)),0,0)), 
fi fi fi fi,
"Экземпляры всего"d10000, 
&unifor('+1W6#'''), 
&unifor('+1W6#',f(rsum((if p(v910^c) then 
                           if v910^a:'U' then f(val(v910^1,|1|n910^1)*val(v910^=,|1|n910^=),0,0)';' else 
                              if 'C 6 2 7':v910^a then else f(val(v910^=,|1|n910^=),0,0)';' 
                        fi fi fi)),0,0)), 
"Студ. ВС всего"d10000, 
&unifor('+7W7#'),&uf('+7W777#'),
if &unifor('IPRIVATE,DATFINOS,')<>'' and val(&unifor('3')) > val(&unifor('IPRIVATE,DATFINOS,')) or 
   &unifor('IPRIVATE,DATFINOS,')='' and val(&unifor('34')) < 9 then 
   if val(&unifor('IMAIN,ACCESSRDR,1'))=1 then 
      &unifor('+7W777#',(if p(v691) then 
                            if p(v691^9) then 
                               &unifor('7RDR,!UPS='v691^9,'!,mfn/')/ 
                               &unifor('7RDR,!?',v691^9,'!,mfn/')/ 
                               &unifor('7RDR,!',&unifor('G2*'v691^9),'!,mfn/')/ 
                         fi fi/)),&uf('+7G777'),
      &uf('+7W7#'f(rsum((if p(g777) then '1;'fi/)),0,0))                      
   else 
      &uf('+7W7#'f(rsum((if p(v691) then 
                            if p(v691^9) then 
                               &unifor('DVUZ,"!',v691^9,'",v68^Z')';' 
                         fi fi)),0,0)), 
fi fi,
"Экземпляры НЕ ВК"d10000, 
&unifor('+1W12#'''), 
&unifor('+1W12#',f(rsum((if p(v910^c) then 
                            if a(v910^t) then 
                               if v910^a:'U' then f(val(v910^1,|1|n910^1)*val(v910^=,|1|n910^=),0,0)';' else 
                                  if 'C 6 2 7':v910^a then else f(val(v910^=,|1|n910^=),0,0)';' 
                         fi fi fi fi)),0,0)), 
|'R1 КО ВС 692-991: '&unifor('+1R1')/'R2 Экз-991: '&unifor('+1R2')/'R3 СТ ВС-991: '&unifor('+1R3')/
'R4 Экз-991 ДОП: '&unifor('+1R4')/'R5 СТ ВС не ВК: '&unifor('+1R5')/ 
'R6 Экз всего: '&unifor('+1R6')/'R7 СТ ВС всего: '&unifor('+1R7')/'R12 ЭКЗ НЕ ВК: '&unifor('+1R12')/|d10000, 

   &unifor('+1R1'), 
   if &unifor('+1R1')='' then 
      if a(v910) and p(v951) then '1' else 
         if &unifor('+1R2')<>'' and &unifor('+1R3')<>'' 
         then f(val(&unifor('+1R2'))/val(&unifor('+1R3')) + 
                val(if s(&unifor('+1R5'),&unifor('+1R7'))<>'' and s(&unifor('+1R4'),&unifor('+1R12'))<>''then 
                    f(val(&unifor('+1R4'),if &unifor('+1R4')='' then &unifor('+1R12') fi)/
                      val(&unifor('+1R5'),if &unifor('+1R5')='' then &unifor('+1R7')fi),0,2)fi),0,2) 
         else 
            if &unifor('+1R5')<>''then 
               if &unifor('+1R4')<>''then f(val(&unifor('+1R4'))/val(&unifor('+1R5')),0,2) 
               else 
                  if &unifor('+1R12')<>''then f(val(&unifor('+1R12'))/val(&unifor('+1R5')),0,2) 
                  else 
                     if &unifor('+1R6')<>''then f(val(&unifor('+1R6'))/val(&unifor('+1R5')),0,2) 
               fi fi fi
            else 
               if &unifor('+1R7')<>''then 
                  f(val(&unifor('+1R4'),if &unifor('+1R4')='' then &unifor('+1R12') fi)/val(&unifor('+1R7')),0,2) 
               else '\qc \b —\b0 ' 
   fi fi fi fi fi 
